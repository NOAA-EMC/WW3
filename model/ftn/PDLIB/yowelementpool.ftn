!PDLIB Software License
!
!Software, as understood herein, shall be broadly interpreted as being inclusive of algorithms,
!source code, object code, data bases and related documentation, all of which shall be furnished
!free of charge to the Licensee. Corrections, upgrades or enhancements may be furnished and, if
!furnished, shall also be furnished to the Licensee without charge. NOAA, however, is not
!required to develop or furnish such corrections, upgrades or enhancements.
!Roland & Partner software, whether that initially furnished or corrections or upgrades,
!are furnished "as is". Roland & Partner furnishes its software without any warranty
!whatsoever and is not responsible for any direct, indirect or consequential damages
!that may be incurred by the Licensee. Warranties of merchantability, fitness for any
!particular purpose, title, and non-infringement, are specifically negated.
!The Licensee is not required to develop any software related to the licensed software.
!However, in the event that the Licensee does so, the Licensee is required to offer same
!to Roland & Partner for inclusion under the instant licensing terms with Roland & Partner
!licensed software along with documentation regarding its principles, use and its advantages.
!This includes changes to the wave model proper including numerical and physical approaches
!to wave modeling, and boundary layer parameterizations embedded in the wave model
!A Licensee may reproduce sufficient software to satisfy its needs.
!All copies shall bear the name of the software with any version number
!as well as replicas of any applied copyright notice, trademark notice,
!other notices and credit lines. Additionally, if the copies have been modified,
!e.g. with deletions or additions, this shall be so stated and identified.
!All of Licensee's employees who have a need to use the software may have access
!to the software but only after reading the instant license and stating, in writing,
!that they have read and understood the license and have agreed to its terms.
!Licensee is responsible for employing reasonable efforts to assure
!that only those of its employees that should have access to the software, in fact, have access.
!The Licensee may use the software for any purpose relating to sea state prediction.
!No disclosure of any portion of the software, whether by means of a media or verbally,
!may be made to any third party by the Licensee or the Licensee's employees
!The Licensee is responsible for compliance with any applicable export or
!import control laws of the United States, the European Union and Germany.
!
!Â© 2009 Roland&Partner, Georgenstr.32, 64297 Germany. All rights reserved.
!PDLIB is a trademark of Roland & Partner. No unauthorized use without permission.
!
module yowElementpool
  implicit none
  private
  public :: finalizeElementpool

  type, public :: t_element
    !> global node id
    !> @todo duplicate date. replace with a pointer to INE(:,) ?
    integer :: node(3)

    contains

    procedure getDomainID
    procedure belongTo
    procedure getArea
  end type

  !> array of all elements, global index
  type(t_element), public, allocatable, target :: elements(:)

  !> number of elements, global
  integer, public :: ne_global = 0

  !> number of local elements
  integer, public :: ne = 0

  !> local element array. it stores the local node IDs
  !> first index from 1 to 3.
  !> second index from 1 to ne.
  !> local node IDs in [1:np]. local ghost IDs in [np+1:np+ng]
  integer, public, target, allocatable :: INE(:,:)

  !> global element array. it stored the global node IDs
  !> first index from 1 to 3.
  !> second index from 1 to ne_global
  integer, public, allocatable :: INE_global(:,:)

  !> Element local to global mapping
  !> ne long. give the global element id
  integer, public, target, allocatable :: ielg(:)

  !> Element global to local mapping
  !> ne_global long. give the local element id but only for this rank. local element id for other ranks are set to 0!
  integer, public, allocatable :: iegl(:)

  contains

  !> Returns the domainID to which this element belongs
  !> conversione: if a element has two nodes from domain 1 and one node from domain 2, the element belongs to domain 1.
  !> If a element adjoint to three different domains, it belongs to the with the lowest domain ID
  !> The one node from domain 2 is, of course, a ghost node.
  function getDomainID(this) result(domainID)
    use yowDatapool, only: myrank, nTasks
    use yowNodepool, only: t_Node, nodes_global
    implicit none
    class(t_element), intent(in) :: this
    integer :: domainID

    integer :: j, itemp, ranks
    type(t_Node), pointer :: node
    type(t_Node) ::  nodes(3)

    domainID = -1

    ! check if this element adjoint to three different domains.
    nodes(:) = nodes_global(this%node(:))
    if(nodes(1)%domainID /= nodes(2)%domainID .and. &
    & nodes(1)%domainID /= nodes(3)%domainID .and. &
    & nodes(2)%domainID /= nodes(3)%domainID) then
      domainID = minval(nodes(:)%domainID)

    ! check if this element has two nodes wich belongs to this domain
    else
      do ranks = 0, nTasks-1
        itemp = 0
        do j=1, 3
          node => nodes_global(this%node(j))
          if(node%domainID == ranks+1) then
            itemp = itemp + 1
          endif
        end do
        ! yes, this element belongs to rank
        if(itemp >= 2) then
          domainID = ranks+1
          exit
        endif
      end do
    endif
  end function

  !> Returns true if the element belongs to rank.
  !> conversione: If a element is connected to domain 1,2 and 3. It belongs to 1,2 and 3.
  !> @param[in] rank optional. If not given, datapool:myrank is used
  function belongTo(this, rank)
    use yowDatapool, only: myrank, nTasks
    use yowNodepool, only: t_Node, nodes_global
    implicit none
    class(t_element), intent(in) :: this
    integer, intent(in), optional :: rank
    logical :: belongTo

    integer :: myDomainID
    type(t_Node) ::  nodes(3)

    belongTo = .false.

    if(present(rank) .eqv. .true.) then
      myDomainID = rank +1
    else
      myDomainID = myrank + 1
    endif

    ! check if this element adjoint to three different domains.
    nodes(:) = nodes_global(this%node(:))
    if(nodes(1)%domainID == myDomainID .or. &
    & nodes(2)%domainID == myDomainID .or. &
    & nodes(3)%domainID == myDomainID) then
      belongTo = .true.
    endif
  end function

  !> Returns the area of this element
  function getArea(this) result(area)
    use yowDatapool, only: myrank, rkind
    use yowNodepool, only: t_Node, nodes_global
    implicit none
    class(t_element), intent(in) :: this
    real(rkind) :: area
    type(t_Node) ::  nodes(3)

    nodes(:) = nodes_global(this%node(:))

    area = 0.5_rkind * ( (nodes(1)%x - nodes(3)%x)*(nodes(2)%y - nodes(1)%y) + &
                 & (nodes(2)%x - nodes(1)%x)*(nodes(3)%y - nodes(1)%y))

!     area = 0.5_rkind * ( (this%p1%getX() - this%p3%getX())*(this%p2%getY() - this%p1%getY()) + &
!                  & (this%p2%getX() - this%p1%getX())*(this%p3%getY() - this%p1%getY()))
  end function


  subroutine finalizeElementpool()
    implicit none

    if(allocated(elements))   deallocate(elements)
    if(allocated(INE))        deallocate(INE)
    if(allocated(INE_global)) deallocate(INE_global)
    if(allocated(ielg))       deallocate(ielg)
    if(allocated(iegl))       deallocate(iegl)
  end subroutine
end module yowElementpool
