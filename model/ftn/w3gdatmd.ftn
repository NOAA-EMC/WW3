!/ ------------------------------------------------------------------- /
      MODULE W3GDATMD
!/
!/                  +-----------------------------------+
!/                  | WAVEWATCH III           NOAA/NCEP |
!/                  |           H. L. Tolman            |
!/                  !           J. H. Alves             !
!/                  |            F. Ardhuin             |
!/                  |                        FORTRAN 90 |
!/                  | Last update :         30-Oct-2009 |
!/                  +-----------------------------------+
!/
!/    24-Jun-2005 : Origination.                        ( version 3.07 )
!/    09-Nov-2005 : Remove soft boundary options.       ( version 3.08 )
!/    23-Jun-2006 : Add data for W3SLN1.                ( version 3.09 )
!/    18-Jul-2006 : Add input grids.                    ( version 3.10 )
!/    05-Oct-2006 : Add filter to array pointers.       ( version 3.10 )
!/    02-Feb-2007 : Add FLAGST.                         ( version 3.10 )
!/    14-Apr-2007 : Add Miche style limiter.            ( version 3.11 )
!/                  ( J. H. Alves )
!/    25-Apr-2007 : Adding Battjes-Janssen Sdb.         ( version 3.11 )
!/                  ( J. H. Alves )
!/    06-Aug-2007 : Fixing SLNP !/SEED bug.             ( version 3.13 )
!/    18-Sep-2007 : Adding WAM4 source terms.           ( version 3.13 )
!/                  ( F. Ardhuin )
!/    15-Apr-2008 : Clean up for distribution.          ( version 3.14 )
!/    27-Jun-2008 : Expand WAM4 variants namelist       ( version 3.14 )
!/                  ( F. Ardhuin )
!/    29-May-2009 : Preparing distribution version.     ( version 3.14 )
!/    30-Oct-2009 : Implement run-time grid selection.  ( version 3.14 )
!/                  (W. E. Rogers & T. J. Campbell, NRL)
!/    30-Oct-2009 : Implement curvilinear grid type.    ( version 3.14 )
!/                  (W. E. Rogers & T. J. Campbell, NRL)
!/    29-Oct-2010 : Implement unstructured grids        ( version 3.14.1 )
!/                  (A. Roland and F. Ardhuin) 
!/
!/    Copyright 2009 National Weather Service (NWS),
!/       National Oceanic and Atmospheric Administration.  All rights
!/       reserved.  WAVEWATCH III is a trademark of the NWS. 
!/       No unauthorized use without permission.
!/
!  1. Purpose :
!
!     Define data structures to set up wave model grids and aliases 
!     to use individual grids transparently. Also includes subroutines
!     to manage data structure and pointing to individual models.
!     Definition of grids and model set up.
!
!  2. Variables and types :
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      NGRIDS    Int.  Public   Number of grids, initialized at -1 
!                               to check proper model initialization.
!      NAUXGR    Int.  Public   Auxiliary grids.
!      IGRID     Int.  Public   Selected spatial grid, init. at -1.
!      ISGRD     Int.  Public   Selected spectral grid, init. at -1.
!      IPARS     Int.  Public   Selected num. and ph. pars, init. at -1.
!      RLGTYPE   I.P.  Public   Named constant for rectilinear grid type
!      CLGTYPE   I.P.  Public   Named constant for curvilinear grid type
!      FLAGLL    Log.  Public   Flag to indicate coordinate system for all grids
!                               .TRUE.: Spherical (lon/lat in degrees)
!                               .FALSE.: Cartesian (meters)
!      GRID      TYPE  Public   Data structure defining grid.
!      GRIDS     GRID  Public   Array of grids.
!      SGRD      TYPE  Public   Data structure defining spectral grid.
!      SGRDS     GRID  Public   Array of spectral grids.
!      MPAR      TYPE  Public   Data structure with all other model
!                               parameters.
!      MPARS     GRID  Public   Array of MPAR.
!     ----------------------------------------------------------------
!
!     All elements of GRID are aliased to pointers with the same
!     name. These pointers are defined as :
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      GTYPE     Int.  Public   Flag for type of grid
!                               RLGTYPE: Rectilinear grid
!                               CLGTYPE: Curvilinear grid
!                               UNGTYPE: Unstructured triangular grid                       
!      GLOBAL    Log.  Public   Flag for global grid (closed parallels).
!      NX, NY    Int.  Public   Discrete dimensions of spatial grid.
!      NSEA(L)   Int.  Public   Number of sea points (local for MPP).
!      TRFLAG    Int.  Public   Flag for use of transparencies
!                                0: No sub-grid obstacles.
!                                1: Obstructions at cell boundaries.
!                                2: Obstructions at cell centers.
!                                3: Like 1 with continuous ice.
!                                4: Like 2 with continuous ice.
!      MAPSTA    I.A.  Public   Grid status map.
!      MAPST2    I.A.  Public   Second grid status map.
!      MAPxx     I.A.  Public   Storage grid maps.
!      SX,SY     Real  Public   Spatial (rectilinear) grid increments.
!      X0,Y0     Real  Public   Lower left corner of spatial (rectilinear) grid.
!      DTCFL     Real  Public   Maximum CFL time step X-Y propagation.
!      DTCFLI    Real  Public   Id. intra-spectral.
!      DTMAX     Real  Public   Maximum overall time step.
!      DTMIN     Real  Public   Minimum dynamic time step for source
!                               term integration.
!      DMIN      Real  Public   Minimum water depth.
!      CTMAX     Real  Public   Maximum CFL number for depth refr.
!      FICE0/N   Real  Public   Cut-off ice conc. for ice coverage.
!      PFMOVE    Real  Public   Tunable parameter in GSE correction
!                               for moving grids.
!      ZB        R.A.  Public   Bottom levels on storage grid.
!      CLATS(I)  R.A.  Public   (Inverse) cosine of latitude at sea points.
!      CTHG0S    R.A.  Public   Constant in great-circle refr. term at sea points.
!      TRNX/Y    R.A.  Public   Transparencies in X/Y for sub-grid
!      X/YGRD    R.A.  Public   Spatial grid coordinate arrays.
!      SX/SYGRD  R.A.  Public   Spatial grid increment arrays.
!      GINIT     Log.  Public   Flag identifying grid initialization.
!      GLOBAL    Log.  Public   Flag for global grid (closed parallels).
!      FLDRY     Log.  Public   Flag for 'dry' run (IO and data
!                               processing only).
!      FLCx      Log.  Public   Flags for prop. is different spaces.
!      FLSOU     Log.  Public   Flag for source term calcualtion.
!      FLAGST    L.A.  Public   Flag for source term computations 
!                               for individual grid points.
!      GNAME     C*30  Public   Grid name.
!      FILEXT    C*10  Public   Extension of WAVEWATCH III file names
!                               default in 'ww3'.
!     ----------------------------------------------------------------
!
!     All elements of SGRD are aliased to pointers with the same
!     name. These pointers are defined as :
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      NK        Int.  Public   Number of discrete wavenumbers.
!      NK2       Int.  Public   Extended wavenumber range.
!      NTH       Int.  Public   Number of discrete directions.
!      NSPEC     Int.  Public   Number of discrete spectral bins.
!      MAPxx     I.A.  Public   Spectral maps.
!      DTH       Real  Public   Directional increments (radians).
!      XFR       Real  Public   Frequency multiplication factor.
!      FR1       Real  Public   Lowest frequency                 (Hz)
!      FTE       Real  Public   Factor in tail integration energy.
!      FTF       Real  Public   Id. frequency.
!      FTWN      Real  Public   Id. wavenumber.
!      FTTR      Real  Public   Id. wave period.
!      FTWL      Real  Public   Id. wave length.
!      FACTIn    Real  Public   Factors for obtaining integer cut-off
!                               frequency.
!      FACHFx    Real  Public   Factor for tail.
!      TH        R.A   Public   Directions (radians).
!      ESIN      R.A   Public   Sine of discrete directions.
!      ECOS      R.A   Public   Cosine of discrete directions.
!      ES2, ESC, EC2
!                R.A   Public   Sine and cosine products
!      SIG       R.A   Public   Relative frequencies (invariant
!                                                     in grid). (rad)
!      SIG2      R.A   Public   Id. for full 2-D spectrum.
!      DSIP      R.A   Public   Frequency bandwidths (prop.)    (rad)
!      DSII      R.A   Public   Frequency bandwidths (int.)     (rad)
!      DDEN      R.A   Public   DSII * DTH * SIG (for integration
!                               based on energy)
!      DDEN2     R.A   Public   Idem, full spectrum.
!      SINIT     Log.  Public   Flag identifying grid initialization.
!     ----------------------------------------------------------------
!
!     The structure MPAR contains all other model parameters for
!     numerical methods and physical parameterizations. It contains
!     itself several structures as outlined below.
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      PINIT     Log.  Public   Flag identifying initialization.
!      NPARS     NPAR  Public   Numerical parameters,
!      PROPS     PROP  Public   Parameters propagatrion schemes.
!      SFLPS     SFLP  Public   Parameters for flux computation.
!      SLNPS     SLNP  Public   Parameters Sln.
!      SRCPS     SRCP  Public   Parameters Sin and Sds.
!      SNLPS     SNLP  Public   Parameters Snl.
!      SBTPS     SBTP  Public   Parameters Sbt.
!      SDBPS     SDBP  Public   Parameters Sdb.
!      STRPS     STRP  Public   Parameters Str.
!      SBSPS     SBSP  Public   Parameters Sbs.
!      SXXPS     SXXP  Public   Parameters Sxx.
!     ----------------------------------------------------------------
!
!     The structure NPAR contains numerical parameters and is aliased
!     as above:
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      FACP      Real  Public   Constant in maximum par. change in
!                               dynamic integration scheme (depends
!                               upon Xp).
!      XREL      Real  Public   Id. relative change.
!      XFLT      Real  Public   Id. filter level.
!      FXFM      Real  Public   Constant for mean frequency in
!                               cut-off.                       (!/ST1)
!      FXPM      Real  Public   Id. PM.
!      XFT       Real  Public   Constant for cut-off freq.     (!/ST2)
!      XFC       Real  Public   Id.
!      FACSD     Real  Public   Constant in seeding algorithm.
!      FHMAX     Real  Public   Hs/depth ratio in limiter     (!/MLIM)
!      RWINDC    Real  Public   Coefficient for current in relative 
!                               wind                          (!/RWND)
!     ----------------------------------------------------------------
!
!     The structure PROP contains parameters for the propagation
!     schemes and is aliased as above:
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      DTME      Real  Public   Swell age in disp. corr.      (!/PR2)
!      CLATMN    Real  Public   Id. minimum cosine of lat.    (!/PR2)
!
!      WDCG      Real  Public   Factors in width of av. Cg.   (!/PR3)
!      WDTH      Real  Public   Factors in width of av. Th.   (!/PR3)
!     ----------------------------------------------------------------
!
!     The structure SFLP contains parameters for the fluxes
!     and is aliased as above:
!     ----------------------------------------------------------------
!                                                            (!/FLX2)
!      NITTIN    Int.  Public   Number of itterations for drag calc.
!      CINXSI    Real  Public   Constant in parametric description
!                                                            (!/FLX3)
!      NITTIN    Int.  Public   Number of itterations for drag calc.
!      CAP_ID    Int   Public   Type of cap used.
!      CINXSI    Real  Public   Constant in parametric description
!      CD_MAX    Real  Public   Cap on Cd.
!     ----------------------------------------------------------------
!
!     The structure SLNP contains parameters for the linear input
!     source terms and is aliased as above:
!
!     ----------------------------------------------------------------
!                                                             (!/LN1)
!      SLNC1     Real  Public   Proportionality and other constants in
!                               input source term.
!      FSPM      Real  Public   Factor for fPM in filter.
!      FSHF      Real  Public   Factor for fh in filter.
!     ----------------------------------------------------------------
!
!     The structure SRCP contains parameters for the input and dis,
!     source terms and is aliased as above:
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      WWNMEANPTAIL R  Public   Power of tail for WNMEAN calculation
!      SSTXFTFTAIL  R  Public   Tail factor for  WNMEAN calculation
!                                                             (!/ST1)
!      SINC1     Real  Public   Proportionality and other constants in
!                               input source term.
!      SDSC1     Real  Public   Combined constant in dissipation
!                               source term.
!                                                             (!/ST2)
!      ZWIND     Real  Public   Height at which the wind is defined
!                               of drag.
!      FSWELL    Real  Public   Reduction factor of negative input
!                               for swell.
!      SHSTAB, OFSTAB, CCNG, CCPS, FFNG, FFPS
!                Real  Public   Factors in effective wind speed.
!      CDSAn     Real  Public   Constants in high-freq. dis.
!      SDSALN    Real  Public   Factor for nondimensional 1-D spectrum.
!      CDSBn     Real  Public   Constants in parameterization of PHI.
!      XFH       Real  Public   Constant for turbulent length scale.
!      XFn       Real  Public   Constants in combining low and high
!                               frequency dissipation.
!                                                             (!/ST3)
!      ZZWND     Real  Public   Height at which the wind is defined
!      AALPHA    Real  Public   Minimum value of charnock parameter
!      BBETA     Real  Public   Wind-wave coupling coefficient
!      ZZALP     Real  Public   Wave age tuning coefficient in Sin
!      TTAUWSHELTER Real  Public Sheltering coefficient for short waves
!      ZZ0MAX    Real  Public   Maximum value of air-side roughness
!      ZZ0RAT    Real  Public   ratio of roughness for mean and
!                               oscillatory flows
!      SSINTHP   Real  Public   Power in cosine of wind input
!      SSWELLF   R.A.  Public   Swell damping coefficients
!      SSDSCn    Real  Public   Dissipation parameters
!      SSDSBR    Real  Public   Threshold in saturation spectrum for Sds
!      SSDSP     Real  Public   Power of B(k) in Sds
!      WWNMEANP  Real  Public   Power that defines the mean wavenumber
!                               in Sds
!      SSTXFTF, SSTXFTWN Real  Public   Tail constants
!      SSDSC4,   Real  Public   Threshold shift in saturation diss.
!      SSDSC5,   Real  Public   Wave-turbulence dissipation factor
!      SSDSC6,   Real  Public   dissipation parameter
!      DDELTA1   Real  Public   Low-frequency dissipation coefficient
!                               in WAM4
!      DDELTA2   Real  Public   High-frequency dissipation coefficient
!                               in WAM4
!      SSDSDTH   Real  Public   Maximum angular sector for saturation
!                               spectrum
!      SSDSCOS   Real  Public   Power of cosine in saturation integral
!      SSDSISO   Int.  Public   Choice of definition of the isotropic
!                               saturation
!     ----------------------------------------------------------------
!
!     The structure SNLP contains parameters for the nonl. inter.
!     source term and is aliased as above:
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!                                                             (!/NL1)
!      SNLC1     Real  Public   Scaled proportionality constant.
!      LAM       Real  Public   Factor defining quadruplet.
!      KDCON     Real  Public   Conversion factor for relative depth.
!      KDMN      Real  Public   Minimum relative depth.
!      SNLSn     Real  Public   Constants in shallow water factor.
!                                                             (!/NL2)
!      IQTPE     Int.  Public   Type of depth treatment 
!                                1 : Deep water         
!                                2 : Deep water / WAM scaling
!                                3 : Finite water depth 
!      NDPTHS    Int.  Public   Number of depth for which integration
!                               space needs to be computed.
!      NLTAIL    Real  Public   Tail factor for parametric tail.
!      DPTHNL    R.A.  Public   Depths corresponding to NDPTHS.   
!                               *** NOTE: This array is not allocated
!                                         in the W3DIMP routine ***
!     ----------------------------------------------------------------
!
!     The structure SBTP contains parameters for the bottom friction
!     source term and is aliased as above:
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      SBTC1     Real  Public   Proportionality constant.    (!/BT1)
!     ----------------------------------------------------------------
!
!     The structure SDBP contains parameters for the depth incduced
!     breaking source term and is aliased as above:
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      SDBC1     Real  Public   Proportionality constant.    (!/DB1)
!      SDBC2     Real  Public   Hmax/d ratio.                (!/DB1)
!      FDONLY    Log.  Public   Flag for checking depth only (!/DB1)
!                               otherwise Miche criterion.
!     ----------------------------------------------------------------
!
!     The structure STRP contains parameters for the triad interaction
!     source term and is aliased as above:
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!     ----------------------------------------------------------------
!
!     The structure SBSP contains parameters for the bottom scattering
!     source term and is aliased as above:
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!     ----------------------------------------------------------------
!
!     The structure SXXP contains parameters for arbitrary source 
!     term and is aliased as above:
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!     ----------------------------------------------------------------
!
!  3. Subroutines and functions :
!
!      Name      Type  Scope    Description
!     ----------------------------------------------------------------
!      W3NMOD    Subr. Public   Set number of grids.
!      W3DIMX    Subr. Public   Set dimensions of spatial grid.
!      W3DIMS    Subr. Public   Set dimensions of spectral grid.
!      W3SETG    Subr. Public   Point to selected grid / model.
!      W3GNTX    Subr. Public   Construct grid arrays
!     ----------------------------------------------------------------
!
!  4. Subroutines and functions used :
!
!      Name      Type  Module   Description
!     ----------------------------------------------------------------
!      STRACE    Subr. W3SERVMD Subroutine tracing.
!      EXTCDE    Subr. W3SERVMD Abort program with exit code.
!     ----------------------------------------------------------------
!
!  5. Remarks :
!
!     - In model versions before 3.06 the parameters in the grid
!       structure were stored in the module W3IOGR.
!     - No subroutine DIMP is provided, instead, arrays are set
!       one-by-one in W3IOGR.
!
!  6. Switches :
!
!     !/PRn  Select propagation scheme
!
!     !/LNn  Select source terms
!     !/STn
!     !/NLn
!     !/BTn
!     !/DBn
!     !/TRn
!     !/BSn
!     !/XXn
!
!     !/S    Enable subroutine tracing.
!     !/T    Enable test output
!
!  7. Source code :
!
!/ ------------------------------------------------------------------- /
!/
!/ Required modules
!/
      USE W3GSRUMD
!/
!/ Specify default accessibility
!/
      PUBLIC
!/
!/ Conventional declarations
!/
      INTEGER                 :: NGRIDS = -1, IGRID = -1, ISGRD = -1, &
                                 IPARS = -1, NAUXGR
!
      INTEGER, PARAMETER      :: RLGTYPE = 1
      INTEGER, PARAMETER      :: CLGTYPE = 2
      INTEGER, PARAMETER      :: UNGTYPE = 3      

      LOGICAL                 :: FLAGLL
!/
!/ Data structures
!/
!/ Grid type
      TYPE GRID
        INTEGER          :: GTYPE
        LOGICAL          :: GLOBAL
        INTEGER          :: NX, NY, NSEA, NSEAL, TRFLAG
        INTEGER, POINTER :: MAPSTA(:,:), MAPST2(:,:),            &
                            MAPFS(:,:), MAPSF(:,:)
        REAL             :: SX, SY, X0, Y0, DTCFL, DTCFLI,       &
                            DTMAX, DTMIN, DMIN, CTMAX,           &
                            FICE0, FICEN, PFMOVE

        REAL   , POINTER :: ZB(:)     ! BOTTOM GRID, DEFINED ON ISEA
        REAL   , POINTER :: CLATS(:)  ! COS(LAT), DEFINED ON SEA POINTS
        REAL   , POINTER :: CLATIS(:) ! INVERSE OF COS(LAT) DEFINED ON ISEA
        REAL   , POINTER :: CTHG0S(:) ! TAN(Y)/R, DEFINED ON ISEA

        REAL   , POINTER :: TRNX(:,:), TRNY(:,:) ! TRANSPARENCY INFORMATION ON IX,IY
        REAL   , POINTER :: XGRD(:,:), YGRD(:,:) ! X AND Y DEFINED ON IX,IY
        REAL   , POINTER :: DXDP(:,:), DXDQ(:,:) ! DX/DP & DX/DQ DEFINED ON IX,IY
        REAL   , POINTER :: DYDP(:,:), DYDQ(:,:) ! DY/DP & DY/DQ DEFINED ON IX,IY
        REAL   , POINTER :: DPDX(:,:), DPDY(:,:) ! DP/DX & DP/DY DEFINED ON IX,IY
        REAL   , POINTER :: DQDX(:,:), DQDY(:,:) ! DQ/DX & DQ/DY DEFINED ON IX,IY
        REAL   , POINTER :: GSQRT(:,:) ! SQRT(G) DEFINED ON IX,IY
        REAL   , POINTER :: HPFAC(:,:) ! H_P = SQRT(G_PP) DEFINED ON IX,IY
        REAL   , POINTER :: HQFAC(:,:) ! H_Q = SQRT(G_QQ) DEFINED ON IX,IY

        LOGICAL          :: GINIT, FLDRY, FLCX, FLCY, FLCTH, FLCK, FLSOU
        LOGICAL, POINTER :: FLAGST(:)
        CHARACTER(LEN=30):: GNAME
        CHARACTER(LEN=10):: FILEXT
        LOGICAL          :: GUGINIT
        REAL, POINTER    :: REFLC(:,:)  ! reflection coefficient
        INTEGER, POINTER :: REFLD(:,:)  ! reflection direction
        INTEGER          :: P2MSF(3), US3DF(3) ! freq. indices for 3D output
!
        TYPE(T_GSU) :: GSU ! Grid search utility object
!
! unstructured data	
!
        LOGICAL               :: CONTOUR
        INTEGER               :: NTRI
        DOUBLE PRECISION, POINTER         :: XYB(:,:)
        INTEGER, POINTER      :: TRIGP(:,:)
        REAL, POINTER         :: LEN(:,:),SI(:), IEN(:,:)

        REAL                  :: MAXX, MAXY, DXYMAX
        REAL, POINTER         :: ANGLE(:,:)
        INTEGER               :: COUNTRI,COUNTOT,NNZ
        INTEGER, POINTER      :: CCON(:), COUNTCON(:), IE_CELL(:),   &
                                 POS_CELL(:), IOBP(:), IOBPD(:,:),   &
                                 IAA(:), JAA(:), POSI(:,:)
        REAL, POINTER         :: TRIA(:), TRIA03(:)
        REAL, POINTER         :: CROSSDIFF(:,:)
! Coupling data
!/COU  LOGICAL                :: COUG_2D, COUG_RAD3D, COUG_US3D
        LOGICAL               :: FLICES   ! flag for source terms in sea ice
        LOGICAL, POINTER      :: RREF(:)
        REAL,    POINTER      :: REFPARS(:)
      END TYPE GRID
!
      TYPE SGRD
        INTEGER               :: NK, NK2, NTH, NSPEC
        INTEGER, POINTER      :: MAPWN(:), MAPTH(:)
        REAL                  :: DTH, XFR, FR1, FTE, FTF, FTWN, FTTR, &
                                 FTWL, FACTI1, FACTI2, FACHFA, FACHFE
        REAL, POINTER         :: TH(:), ESIN(:), ECOS(:), ES2(:),     &
                                 ESC(:), EC2(:), SIG(:), SIG2(:),     &
                                 DSIP(:), DSII(:), DDEN(:), DDEN2(:)
        LOGICAL               :: SINIT
      END TYPE SGRD
!
      TYPE NPAR
        REAL                  :: FACP, XREL, XFLT, FXFM, FXPM,        &
                                 XFT, XFC, FACSD, FHMAX
!/RWND        REAL            :: RWINDC
      END TYPE NPAR
!
      TYPE PROP
!/PR0        REAL                  :: DUMMY
!/PR1        REAL                  :: DUMMY
!/PR2        REAL                  :: DTME, CLATMN
!/PR3        REAL                  :: WDCG, WDTH
      END TYPE PROP
!
      TYPE SFLP
!/FLX0        REAL                  :: DUMMY
!/FLX1        REAL                  :: DUMMY
!/FLX2        INTEGER               :: NITTIN
!/FLX2        REAL                  :: CINXSI
!/FLX3        INTEGER               :: NITTIN, CAP_ID
!/FLX3        REAL                  :: CINXSI, CD_MAX
!/FLXX        REAL                  :: DUMMY
      END TYPE SFLP
!
      TYPE SLNP
!/SEED        REAL                  :: DUMMY
!/LN0        REAL                  :: DUMMY
!/LN1        REAL                  :: SLNC1, FSPM, FSHF
!/LNX        REAL                  :: DUMMY
      END TYPE SLNP
!
      TYPE SRCP
        REAL                       :: WWNMEANPTAIL, SSTXFTFTAIL
!/ST1        REAL                  :: SINC1, SDSC1
!/ST2        REAL                  :: ZWIND, FSWELL, SHSTAB,               &
!/ST2                                 OFSTAB, CCNG, CCPS, FFNG, FFPS,      &
!/ST2                                 CDSA0, CDSA1, CDSA2, SDSALN,         &
!/ST2                                 CDSB0, CDSB1, CDSB2, CDSB3, FPIMIN,  &
!/ST2                                 XFH, XF1, XF2
!/ST3        INTEGER               :: SSDSISO, SSDSBRFDF
!/ST3        REAL                  :: AALPHA, BBETA, ZZ0MAX, ZZ0RAT, ZZALP,&
!/ST3                                 SSINTHP, TTAUWSHELTER, SSWELLF(1:6), &
!/ST3                                 SSDSC1, SSDSC2, SSDSC3, SSDSBR,      &
!/ST3                                 SSDSP, WWNMEANP, SSTXFTF, SSTXFTWN,  &
!/ST3                                 FFXPM, FFXFM,                        &
!/ST3                                 SSDSC4, SSDSC5, SSDSC6, DDELTA1,     &
!/ST3                                 DDELTA2, ZZWND
!
!/ST4        INTEGER               :: SSWELLFPAR, SSDSISO, SSDSBRFDF
!/ST4        INTEGER,  POINTER     :: IKTAB(:,:), SATINDICES(:,:)
!/ST4        REAL,     POINTER     :: DCKI(:,:), SATWEIGHTS(:,:),CUMULW(:,:,:,:)
!/ST4        REAL                  :: AALPHA, BBETA, ZZ0MAX, ZZ0RAT, ZZALP,&
!/ST4                                 SSINTHP, TTAUWSHELTER, SSWELLF(1:7), &
!/ST4                                 SSDSC1, SSDSC2, SSDSC3, SSDSBR,      &
!/ST4                                 SSDSP, WWNMEANP, SSTXFTF, SSTXFTWN,  &
!/ST4                                 FFXPM, FFXFM, SSDSBRF1, SSDSBRF2,    &
!/ST4                                 SSDSBINT,SSDSBCK,SSDSHCK, SSDSABK,   &
!/ST4                                 SSDSPBK, SSDSC4, SSDSC5, SSDSC6
!/ST4        REAL                  :: ZZWND
!/ST4        REAL                  :: SSDSCOS, SSDSDTH, SSDSBR2, SSDSBM(0:4)
!/STX        REAL                  :: DUMMY
      END TYPE SRCP
!
      TYPE SNLP
!/NL0        REAL                  :: DUMMY
!/NL1        REAL                  :: SNLC1, LAM, KDCON, KDMN,             &
!/NL1                                 SNLS1, SNLS2, SNLS3
!/NL2        INTEGER               :: IQTPE, NDPTHS
!/NL2        REAL                  :: NLTAIL
!/NL2        REAL, POINTER         :: DPTHNL(:)
!/NLX        REAL                  :: DUMMY
      END TYPE SNLP
!
      TYPE SBTP
!/BT0        REAL                  :: DUMMY
!/BT1        REAL                  :: SBTC1
!/BTX        REAL                  :: DUMMY
      END TYPE SBTP
!
      TYPE SDBP
!/DB0        REAL                  :: DUMMY
!/DB1        REAL                  :: SDBC1, SDBC2
!/DB1        LOGICAL               :: FDONLY
!/DBX        REAL                  :: DUMMY
      END TYPE SDBP
!
      TYPE STRP
!/TR0        REAL                  :: DUMMY
!/TRX        REAL                  :: DUMMY
      END TYPE STRP
!
      TYPE SBSP
!/BS0        REAL                  :: DUMMY
!/BS1        REAL                  :: DUMMY
!/BSX        REAL                  :: DUMMY
      END TYPE SBSP
!
      TYPE SXXP
!/XX0        REAL                  :: DUMMY
!/XXX        REAL                  :: DUMMY
      END TYPE SXXP
      
! specific type for unstructured scheme
      TYPE SCHM
         LOGICAL              :: FSN,FSPSI,FSFCT,FSNIMP
      END TYPE SCHM    
!      
!
      TYPE MPAR
        LOGICAL               :: PINIT
        TYPE(NPAR)            :: NPARS
        TYPE(PROP)            :: PROPS
        TYPE(SFLP)            :: SFLPS
        TYPE(SLNP)            :: SLNPS
        TYPE(SRCP)            :: SRCPS
        TYPE(SNLP)            :: SNLPS
        TYPE(SBTP)            :: SBTPS
        TYPE(SDBP)            :: SDBPS
        TYPE(STRP)            :: STRPS
        TYPE(SBSP)            :: SBSPS
        TYPE(SXXP)            :: SXXPS
        TYPE(SCHM)            :: SCHMS
      END TYPE MPAR
!/
!/ Data storage
!/
      TYPE(GRID), TARGET, ALLOCATABLE :: GRIDS(:)
      TYPE(SGRD), TARGET, ALLOCATABLE :: SGRDS(:)
      TYPE(MPAR), TARGET, ALLOCATABLE :: MPARS(:)
!/
!/ Data aliasses for structure GRID(S)
!/
      INTEGER, POINTER :: GTYPE
      LOGICAL, POINTER :: GLOBAL
      INTEGER, POINTER        :: NX, NY, NSEA, NSEAL, TRFLAG
      INTEGER, POINTER        :: P2MSF(:),US3DF(:)
      REAL,    POINTER        :: REFLC(:,:)
      INTEGER, POINTER        :: REFLD(:,:)    
!
! Variables for unstructured grids 
!
      LOGICAL, POINTER        :: CONTOUR       
      INTEGER, POINTER        :: NTRI,COUNTRI,COUNTOT,NNZ
!  XYB may not be necessary now that we have XGRD and YGRD
!  but these XGRD and YGRD should probably be double precision
      DOUBLE PRECISION, POINTER  ::     XYB(:,:)   
      INTEGER, POINTER        :: TRIGP(:,:)
      REAL, POINTER           :: IEN(:,:), LEN(:,:), SI(:)
      REAL, POINTER           :: ANGLE(:,:)
      INTEGER,  POINTER       :: CCON(:),  COUNTCON(:), IE_CELL(:),    &
                                 POS_CELL(:), IOBP(:), IOBPD(:,:),     &
                                 IAA(:), JAA(:), POSI(:,:)
      REAL, POINTER           :: TRIA(:), TRIA03(:)
      REAL, POINTER           :: CROSSDIFF(:,:)
      REAL,POINTER            :: MAXX, MAXY, DXYMAX 
      LOGICAL, POINTER        :: GUGINIT       
!
      LOGICAL, POINTER        :: COUG_2D, COUG_RAD3D, COUG_US3D
      LOGICAL, POINTER        :: FLICES, RREF(:)
      REAL,    POINTER        :: REFPARS(:)
      INTEGER, POINTER        :: MAPSTA(:,:), MAPST2(:,:),            &
                                 MAPFS(:,:), MAPSF(:,:)
      REAL, POINTER           :: SX, SY, X0, Y0, DTCFL, DTCFLI,       &
                                 DTMAX, DTMIN, DMIN, CTMAX,           &
                                 FICE0, FICEN, PFMOVE
      REAL, POINTER           :: ZB(:), CLATS(:)
      REAL   , POINTER :: CLATIS(:) ! INVERSE OF COS(LAT) DEFINED ON ISEA
      REAL   , POINTER :: CTHG0S(:) ! TAN(Y)/R, DEFINED ON ISEA

      REAL   , POINTER :: TRNX(:,:), TRNY(:,:) ! TRANSPARENCY INFORMATION ON IX,IY
      REAL   , POINTER :: XGRD(:,:), YGRD(:,:) ! X AND Y DEFINED ON IX,IY
      REAL   , POINTER :: DXDP(:,:), DXDQ(:,:) ! DX/DP & DX/DQ DEFINED ON IX,IY
      REAL   , POINTER :: DYDP(:,:), DYDQ(:,:) ! DY/DP & DY/DQ DEFINED ON IX,IY
      REAL   , POINTER :: DPDX(:,:), DPDY(:,:) ! DP/DX & DP/DY DEFINED ON IX,IY
      REAL   , POINTER :: DQDX(:,:), DQDY(:,:) ! DQ/DX & DQ/DY DEFINED ON IX,IY
      REAL   , POINTER :: GSQRT(:,:) ! SQRT(G) DEFINED ON IX,IY
      REAL   , POINTER :: HPFAC(:,:) ! H_P = SQRT(G_PP) DEFINED ON IX,IY
      REAL   , POINTER :: HQFAC(:,:) ! H_Q = SQRT(G_QQ) DEFINED ON IX,IY

      LOGICAL, POINTER :: GINIT, FLDRY, FLCX, FLCY, FLCTH, FLCK, FLSOU
      LOGICAL, POINTER :: FLAGST(:)

      CHARACTER(LEN=30), POINTER :: GNAME
      CHARACTER(LEN=10), POINTER :: FILEXT

      TYPE(T_GSU), POINTER :: GSU ! Grid search utility object
!/
!/ Data aliasses for structure SGRD(S)
!/
      INTEGER, POINTER        :: NK, NK2, NTH, NSPEC
      INTEGER, POINTER        :: MAPWN(:), MAPTH(:)
      REAL, POINTER           :: DTH, XFR, FR1, FTE, FTF, FTWN, FTTR, &
                                 FTWL, FACTI1, FACTI2, FACHFA, FACHFE
      REAL, POINTER           :: TH(:), ESIN(:), ECOS(:), ES2(:),     &
                                 ESC(:), EC2(:), SIG(:), SIG2(:),     &
                                 DSIP(:), DSII(:), DDEN(:), DDEN2(:)
      LOGICAL, POINTER        :: SINIT
!/
!/ Data aliasses for structure MPAR(S)
!/
      LOGICAL, POINTER        :: PINIT
!/
!/ Data aliasses for structure NPAR(S)
!/
      REAL, POINTER           :: FACP, XREL, XFLT, FXFM, FXPM,        &
                                 XFT, XFC, FACSD, FHMAX
!/RWND REAL, POINTER           :: RWINDC
!/
!/ Data aliasses for structure PROP(S)
!/
!/PR2      REAL, POINTER           :: DTME, CLATMN
!/PR3      REAL, POINTER           :: WDCG, WDTH
!/
!/ Data aliasses for structure SFLP(S)
!/
!/FLX2      INTEGER, POINTER        :: NITTIN
!/FLX2      REAL, POINTER           :: CINXSI
!/FLX3      INTEGER, POINTER        :: NITTIN, CAP_ID
!/FLX3      REAL, POINTER           :: CINXSI, CD_MAX
!/
!/ Data aliasses for structure SLNP(S)
!/
!/LN1      REAL, POINTER           :: SLNC1, FSPM, FSHF
!/
!/ Data aliasses for structure SRCP(S)
!/
!/ST1      REAL, POINTER           :: SINC1, SDSC1
!/ST2      REAL, POINTER           :: ZWIND, FSWELL, SHSTAB,               &
!/ST2                                 OFSTAB, CCNG, CCPS, FFNG, FFPS,      &
!/ST2                                 CDSA0, CDSA1, CDSA2, SDSALN,         &
!/ST2                                 CDSB0, CDSB1, CDSB2, CDSB3, FPIMIN,  &
!/ST2                                 XFH, XF1, XF2
!/ST3      REAL, POINTER           :: ZZWND, AALPHA, BBETA, ZZ0MAX, ZZ0RAT,&
!/ST3                                 ZZALP, FFXFM, FFXPM,                 &
!/ST3                                 SSINTHP, TTAUWSHELTER, SSWELLF(:),   &
!/ST3                                 SSDSC1, SSDSC2, SSDSC3, SSDSBR,      &
!/ST3                                 SSDSP, WWNMEANP, SSTXFTF, SSTXFTWN,  &
!/ST3                                 SSDSC4, SSDSC5, SSDSC6, SSDSBR2,     &
!/ST3                                 DDELTA1, DDELTA2,                    &
!/ST3                                 SSDSCOS, SSDSDTH, SSDSBM(:)
!/ST4      INTEGER, POINTER        :: SSWELLFPAR, SSDSISO,SSDSBRFDF,       &
!/ST4                                 IKTAB(:,:), SATINDICES(:,:)
!/ST4      REAL, POINTER           :: DCKI(:,:), SATWEIGHTS(:,:),CUMULW(:,:,:,:)
!/ST4      REAL, POINTER           :: ZZWND, AALPHA, BBETA, ZZ0MAX, ZZ0RAT, ZZALP, &
!/ST4                                 FFXFM, FFXPM,SSDSBRF1, SSDSBRF2, &
!/ST4                                 SSDSBINT, SSDSBCK, SSDSHCK, SSDSABK, SSDSPBK, &
!/ST4                                 SSINTHP, TTAUWSHELTER, SSWELLF(:),   &
!/ST4                                 SSDSC1, SSDSC2, SSDSC3, SSDSBR,      &
!/ST4                                 SSDSP, WWNMEANP, SSTXFTF, SSTXFTWN,  &
!/ST4                                 SSDSC4, SSDSC5, SSDSC6, SSDSBR2,     &
!/ST4                                 SSDSCOS, SSDSDTH, SSDSBM(:)
      REAL, POINTER           :: WWNMEANPTAIL, SSTXFTFTAIL
!/
!/ Data aliasses for structure SNLP(S)
!/
!/NL1      REAL, POINTER           :: SNLC1, LAM, KDCON, KDMN,             &
!/NL1                                 SNLS1, SNLS2, SNLS3
!/NL2      INTEGER, POINTER        :: IQTPE, NDPTHS
!/NL2      REAL, POINTER           :: NLTAIL
!/NL2      REAL, POINTER           :: DPTHNL(:)
!/
!/ Data aliasses for structure SBTP(S)
!/
!/BT1      REAL, POINTER           :: SBTC1
!/
!/ Data aliasses for structure SDBP(S)
!/
!/DB1      REAL, POINTER           :: SDBC1, SDBC2
!/DB1      LOGICAL, POINTER        :: FDONLY
!/
!/ Data aliasing for structure SCHM(S)
      LOGICAL, POINTER        :: FSN,FSPSI,FSFCT,FSNIMP
!/
      CONTAINS
!/ ------------------------------------------------------------------- /
      SUBROUTINE W3NMOD ( NUMBER, NDSE, NDST, NAUX )
!/
!/                  +-----------------------------------+
!/                  | WAVEWATCH III           NOAA/NCEP |
!/                  |           H. L. Tolman            |
!/                  |                        FORTRAN 90 |
!/                  | Last update :         18-Jul-2006 !
!/                  +-----------------------------------+
!/
!/    24-Feb-2004 : Origination.                        ( version 3.06 )
!/    18-Jul-2006 : Add input grids.                    ( version 3.10 )
!/
!  1. Purpose :
!
!     Set up the number of grids to be used.
!
!  2. Method :
!
!     Store in NGRIDS and allocate GRIDS.
!
!  3. Parameters :
!
!     Parameter list
!     ----------------------------------------------------------------
!       NUMBER  Int.   I   Number of grids to be used.
!       NDSE    Int.   I   Error output unit number.
!       NDST    Int.   I   Test output unit number.
!       NAUX    Int.   I   Number of auxiliary grids to be used.
!                          Grids -NAUX:NUBMER are defined, optional
!                          parameters.
!     ----------------------------------------------------------------
!
!  4. Subroutines used :
!
!     See module documentation.
!
!  5. Called by :
!
!     Any program that uses this grid structure.
!
!  6. Error messages :
!
!     - Error checks on previous setting of variable.
!
!  7. Remarks :
!
!  8. Structure :
!
!  9. Switches :
!
!     !/S    Enable subroutine tracing.
!     !/T    Enable test output
!
! 10. Source code :
!
!/ ------------------------------------------------------------------- /
      USE W3SERVMD, ONLY: EXTCDE
!/S      USE W3SERVMD, ONLY: STRACE
!
      IMPLICIT NONE
!/
!/ ------------------------------------------------------------------- /
!/ Parameter list
!/
      INTEGER, INTENT(IN)           :: NUMBER, NDSE, NDST
      INTEGER, INTENT(IN), OPTIONAL :: NAUX
!/
!/ ------------------------------------------------------------------- /
!/ Local parameters
!/
      INTEGER                 :: I, NLOW
!/S      INTEGER, SAVE           :: IENT = 0
!/
!/S      CALL STRACE (IENT, 'W3NMOD')
!
! -------------------------------------------------------------------- /
! 1.  Test input and module status
!
      IF ( NGRIDS .NE. -1 ) THEN
          WRITE (NDSE,1001) NGRIDS
          CALL EXTCDE (1)
        END IF
!
      IF ( NUMBER .LT. 1 ) THEN
          WRITE (NDSE,1002) NUMBER
          CALL EXTCDE (2)
        END IF
!
      IF ( PRESENT(NAUX) ) THEN
          NLOW   = -NAUX
        ELSE
          NLOW   = 1
        END IF
!
      IF ( NLOW .GT. 1 ) THEN
          WRITE (NDSE,1003) -NLOW
          CALL EXTCDE (3)
        END IF
!
! -------------------------------------------------------------------- /
! 1.  Set variable and allocate arrays
!
      NGRIDS = NUMBER
      NAUXGR = - NLOW
      ALLOCATE ( GRIDS(NLOW:NUMBER) )
      ALLOCATE ( SGRDS(NLOW:NUMBER) )
      ALLOCATE ( MPARS(NLOW:NUMBER) )
!
! -------------------------------------------------------------------- /
! 2.  Initialize GINIT adn SINIT
!
      DO I=NLOW, NUMBER
        GRIDS(I)%GINIT  = .FALSE.
        SGRDS(I)%SINIT  = .FALSE.
        MPARS(I)%PINIT  = .FALSE.
!/NL2        MPARS(I)%SNLPS%NDPTHS = 0
        END DO
!
!/T      WRITE (NDST,9000) NLOW, NGRIDS
!
      RETURN
!
! Formats
!
 1001 FORMAT (/' *** ERROR W3NMOD : GRIDS ALREADY INITIALIZED *** '/  &
               '                    NGRIDS = ',I10/)
 1002 FORMAT (/' *** ERROR W3NMOD : ILLEGAL NUMBER OF GRIDS *** '/    &
               '                    NUMBER = ',I10/)
 1003 FORMAT (/' *** ERROR W3NMOD : ILLEGAL NUMBER OF AUX GRIDS *** '/&
               '                    NUMBER = ',I10/)
!
!/T 9000 FORMAT (' TEST W3NMOD : SETTING UP FOR GRIDS ',I3,           &
!/T              ' THROUGH ',I3)
!
!/
!/ End of W3NMOD ----------------------------------------------------- /
!/
      END SUBROUTINE W3NMOD
!/ ------------------------------------------------------------------- /
      SUBROUTINE W3DIMX  ( IMOD, MX, MY, MSEA, NDSE, NDST )
!/
!/                  +-----------------------------------+
!/                  | WAVEWATCH III           NOAA/NCEP |
!/                  |           H. L. Tolman            |
!/                  |                        FORTRAN 90 |
!/                  | Last update :         30-Oct-2010 |
!/                  +-----------------------------------+
!/
!/    24-Jun-2005 : Origination.                        ( version 3.07 )
!/    18-Jul-2006 : Add input grids.                    ( version 3.10 )
!/    05-Oct-2006 : Add filter to array pointers.       ( version 3.10 )
!/    02-Feb-2007 : Add FLAGST.                         ( version 3.10 )
!/    30-Oct-2009 : Implement run-time grid selection.  ( version 3.14 )
!/                  (W. E. Rogers & T. J. Campbell, NRL)
!/    30-Oct-2009 : Implement curvilinear grid type.    ( version 3.14 )
!/                  (W. E. Rogers & T. J. Campbell, NRL)
!/    30-Oct-2009 : Implement unstructured grids        ( version 3.14.1)
!/
!  1. Purpose :
!
!     Initialize an individual spatial grid at the proper dimensions.
!
!  2. Method :
!
!     Allocate directly into the structure array GRIDS. Note that
!     this cannot be done through the pointer alias!
!
!  3. Parameters :
!
!     Parameter list
!     ----------------------------------------------------------------
!       IMOD    Int.   I   Model number to point to.
!       NDSE    Int.   I   Error output unit number.
!       NDST    Int.   I   Test output unit number.
!       MX, MY, MSEA       Like NX, NY, NSEA in data structure.
!     ----------------------------------------------------------------
!
!  4. Subroutines used :
!
!       See module documentation.
!
!  5. Called by :
!
!      Name      Type  Module   Description
!     ----------------------------------------------------------------
!      W3IOGR    Subr. W3IOGRMD Model definition file IO program.
!      WW3_GRID  Prog.   N/A    Model set up program.
!     ----------------------------------------------------------------
!
!  6. Error messages :
!
!     - Check on input parameters.
!     - Check on previous allocation.
!
!  7. Remarks :
!
!     - Grid dimensions apre passed through parameter list and then 
!       locally stored to assure consistency between allocation and
!       data in structure.
!     - W3SETG needs to be called after allocation to point to 
!       proper allocated arrays.
!
!  8. Structure :
!
!     See source code.
!
!  9. Switches :
!
!     !/S    Enable subroutine tracing.
!     !/T    Enable test output
!
! 10. Source code :
!
!/ ------------------------------------------------------------------- /
      USE W3SERVMD, ONLY: EXTCDE
!/S      USE W3SERVMD, ONLY: STRACE
!
      IMPLICIT NONE
!
!/
!/ ------------------------------------------------------------------- /
!/ Parameter list
!/
      INTEGER, INTENT(IN)     :: IMOD, MX, MY, MSEA, NDSE, NDST
!/
!/ ------------------------------------------------------------------- /
!/ Local parameters
!/
!/S      INTEGER, SAVE           :: IENT = 0
!/
!/S      CALL STRACE (IENT, 'W3DIMX')
!
! -------------------------------------------------------------------- /
! 1.  Test input and module status
!
      IF ( NGRIDS .EQ. -1 ) THEN
          WRITE (NDSE,1001)
          CALL EXTCDE (1)
        END IF
!
      IF ( IMOD.LT.-NAUXGR .OR. IMOD.GT.NGRIDS ) THEN
          WRITE (NDSE,1002) IMOD, -NAUXGR, NGRIDS
          CALL EXTCDE (2)
        END IF
!
      IF ( MX.LT.3 .OR. (MY.LT.3.AND.GTYPE.NE.UNGTYPE) .OR. MSEA.LT.1 ) THEN
        WRITE (NDSE,1003) MX, MY, NSEA, NSEAL
        CALL EXTCDE (3)
        END IF
!
      IF ( GRIDS(IMOD)%GINIT ) THEN
          WRITE (NDSE,1004)
          CALL EXTCDE (4)
        END IF
!
!/T      WRITE (NDST,9000) IMOD, MX, MY, MSEA
!
! -------------------------------------------------------------------- /
! 2.  Allocate arrays
!
! NB: Some array start at 0 becase MAPFS(IY,IX)=0 for missing points 
!
      ALLOCATE ( GRIDS(IMOD)%MAPSTA(MY,MX),  &
                   GRIDS(IMOD)%MAPST2(MY,MX),  &
                   GRIDS(IMOD)%MAPFS(MY,MX),   &
                   GRIDS(IMOD)%MAPSF(MSEA,3),  &
                   GRIDS(IMOD)%FLAGST(MSEA),   &
                   GRIDS(IMOD)%ZB(MSEA),       &
                   GRIDS(IMOD)%CLATS(0:MSEA),    &
                   GRIDS(IMOD)%CLATIS(0:MSEA),   &
                   GRIDS(IMOD)%CTHG0S(0:MSEA),   &
                   GRIDS(IMOD)%TRNX(MY,MX),    &
                   GRIDS(IMOD)%TRNY(MY,MX),    &
                   GRIDS(IMOD)%XGRD(MY,MX),    &
                   GRIDS(IMOD)%YGRD(MY,MX),    &
                   GRIDS(IMOD)%DXDP(MY,MX),    &
                   GRIDS(IMOD)%DXDQ(MY,MX),    &
                   GRIDS(IMOD)%DYDP(MY,MX),    &
                   GRIDS(IMOD)%DYDQ(MY,MX),    &
                   GRIDS(IMOD)%DPDX(MY,MX),    &
                   GRIDS(IMOD)%DPDY(MY,MX),    &
                   GRIDS(IMOD)%DQDX(MY,MX),    &
                   GRIDS(IMOD)%DQDY(MY,MX),    &
                   GRIDS(IMOD)%GSQRT(MY,MX),   &
                   GRIDS(IMOD)%HPFAC(MY,MX),   &
                   GRIDS(IMOD)%HQFAC(MY,MX)    )
!
      GRIDS(IMOD)%FLAGST = .TRUE.
      GRIDS(IMOD)%GINIT  = .TRUE.
      GRIDS(IMOD)%CLATS(0)=1.
      GRIDS(IMOD)%CLATIS(0)=1.
!
      ALLOCATE ( GRIDS(IMOD)%RREF(4)  )
      ALLOCATE ( GRIDS(IMOD)%REFPARS(6) )
      ALLOCATE ( GRIDS(IMOD)%REFLC(4,0:NSEA))
      ALLOCATE ( GRIDS(IMOD)%REFLD(4,0:NSEA))
      GRIDS(IMOD)%REFLC(1:4,0:NSEA)=0.
      GRIDS(IMOD)%REFLD(:,:)=0
!/T      WRITE (NDST,9001)
!
! -------------------------------------------------------------------- /
! 2.  Update counters in grid
!
      GRIDS(IMOD)%NX     = MX
      GRIDS(IMOD)%NY     = MY
      GRIDS(IMOD)%NSEA   = MSEA
!
!/T      WRITE (NDST,9002)
!
! -------------------------------------------------------------------- /
! 3.  Point to allocated arrays
!
      CALL W3SETG ( IMOD, NDSE, NDST )
!
!/T      WRITE (NDST,9003)
!
      RETURN
!
! Formats
!
 1001 FORMAT (/' *** ERROR W3DIMX : GRIDS NOT INITIALIZED *** '/      &
               '                    RUN W3NMOD FIRST '/)
 1002 FORMAT (/' *** ERROR W3DIMX : ILLEGAL MODEL NUMBER *** '/       &
               '                    IMOD   = ',I10/                   &
               '                    NAUXGR = ',I10/                   &
               '                    NGRIDS = ',I10/)
 1003 FORMAT (/' *** ERROR W3DIMX : ILLEGAL GRID DIMENSION(S) *** '/  &
               '                    INPUT = ',I10/)
 1004 FORMAT (/' *** ERROR W3DIMX : ARRAY(S) ALREADY ALLOCATED *** ')
!
!/T 9000 FORMAT (' TEST W3DIMX : MODEL ',I4,' DIM. AT ',2I5,I7)
!/T 9001 FORMAT (' TEST W3DIMX : ARRAYS ALLOCATED')
!/T 9002 FORMAT (' TEST W3DIMX : DIMENSIONS STORED')
!/T 9003 FORMAT (' TEST W3DIMX : POINTERS RESET')
!/
!/ End of W3DIMX ----------------------------------------------------- /
!/
      END SUBROUTINE W3DIMX
!/ ------------------------------------------------------------------- /
      SUBROUTINE W3DIMS  ( IMOD, MK, MTH, NDSE, NDST )
!/
!/                  +-----------------------------------+
!/                  | WAVEWATCH III           NOAA/NCEP |
!/                  |           H. L. Tolman            |
!/                  |                        FORTRAN 90 |
!/                  | Last update :         05-Oct-2006 !
!/                  +-----------------------------------+
!/
!/    19-Feb-2004 : Origination.                        ( version 3.06 )
!/    18-Jul-2006 : Add input grids.                    ( version 3.10 )
!/    05-Oct-2006 : Add filter to array pointers.       ( version 3.10 )
!/
!  1. Purpose :
!
!     Initialize an individual spatial grid at the proper dimensions.
!
!  2. Method :
!
!     Allocate directly into the structure array GRIDS. Note that
!     this cannot be done through the pointer alias!
!
!  3. Parameters :
!
!     Parameter list
!     ----------------------------------------------------------------
!       IMOD    Int.   I   Model number to point to.
!       NDSE    Int.   I   Error output unit number.
!       MK,MTH  Int.   I   Spectral dimensions.
!       NDST    Int.   I   Test output unit number.
!     ----------------------------------------------------------------
!
!  4. Subroutines used :
!
!     See module documentation.
!
!  5. Called by :
!
!      Name      Type  Module   Description
!     ----------------------------------------------------------------
!      W3IOGR    Subr. W3IOGRMD Model definition file IO program.
!      WW3_GRID  Prog.   N/A    Model set up program.
!     ----------------------------------------------------------------
!
!  6. Error messages :
!
!     - Check on input parameters.
!     - Check on previous allocation.
!
!  7. Remarks :
!
!     - Grid dimensions apre passed through parameter list and then 
!       locally stored to assure consistency between allocation and
!       data in structure.
!     - W3SETG needs to be called after allocation to point to 
!       proper allocated arrays.
!
!  8. Structure :
!
!     See source code.
!
!  9. Switches :
!
!     !/S    Enable subroutine tracing.
!     !/T    Enable test output
!
! 10. Source code :
!
!/ ------------------------------------------------------------------- /
      USE W3SERVMD, ONLY: EXTCDE
!/ST4  USE CONSTANTS, ONLY: RADE
!/S      USE W3SERVMD, ONLY: STRACE
!
      IMPLICIT NONE
!
!/
!/ ------------------------------------------------------------------- /
!/ Parameter list
!/
      INTEGER, INTENT(IN)     :: IMOD, MK, MTH, NDSE, NDST
!/
!/ ------------------------------------------------------------------- /
!/ Local parameters
!/
      INTEGER, SAVE           :: MK2, MSPEC
!/ST4  INTEGER                  :: SDSNTH
!/S      INTEGER, SAVE           :: IENT = 0
!/
!/S      CALL STRACE (IENT, 'W3DIMS')
!
! -------------------------------------------------------------------- /
! 1.  Test input and module status
!
      IF ( NGRIDS .EQ. -1 ) THEN
          WRITE (NDSE,1001)
          CALL EXTCDE (1)
        END IF
!
      IF ( IMOD.LT.-NAUXGR .OR. IMOD.GT.NGRIDS ) THEN
          WRITE (NDSE,1002) IMOD, -NAUXGR, NGRIDS
          CALL EXTCDE (2)
        END IF
!
      IF ( MK.LT.3 .OR. MTH.LT.4 ) THEN
          WRITE (NDSE,1003) MK, MTH
          CALL EXTCDE (3)
        END IF
!
      IF ( SGRDS(IMOD)%SINIT ) THEN
          WRITE (NDSE,1004)
          CALL EXTCDE (4)
        END IF
!
      MK2    = MK + 2
      MSPEC  = MK * MTH
!
!/T      WRITE (NDST,9000) IMOD, MTH, MK, MK2, MSPEC
!
! -------------------------------------------------------------------- /
! 2.  Allocate arrays
!
      ALLOCATE ( SGRDS(IMOD)%MAPWN(MSPEC+MTH),                        &
                 SGRDS(IMOD)%MAPTH(MSPEC+MTH),                        &
                 SGRDS(IMOD)%TH(MTH),                                 &
                 SGRDS(IMOD)%ESIN(MSPEC+MTH),                         &
                 SGRDS(IMOD)%ECOS(MSPEC+MTH),                         &
                 SGRDS(IMOD)%ES2(MSPEC+MTH),                          &
                 SGRDS(IMOD)%ESC(MSPEC+MTH),                          &
                 SGRDS(IMOD)%EC2(MSPEC+MTH),                          &
                 SGRDS(IMOD)%SIG(0:MK+1),                             &
                 SGRDS(IMOD)%SIG2(MSPEC),                             &
                 SGRDS(IMOD)%DSIP(0:MK+1),                            &
                 SGRDS(IMOD)%DSII(MK),                                &
                 SGRDS(IMOD)%DDEN(MK),                                &
                 SGRDS(IMOD)%DDEN2(MSPEC)  )
!/ST4 ALLOCATE(MPARS(IMOD)%SRCPS%IKTAB(MK,2000))
!/ST4 ALLOCATE(MPARS(IMOD)%SRCPS%DCKI(2000,1300))
!/ST4  SDSNTH  = MTH/2-1 !MIN(NINT(SSDSDTH/(DTH*RADE)),MTH/2-1)
!/ST4 ALLOCATE(MPARS(IMOD)%SRCPS%SATINDICES(MTH,2*SDSNTH+1))
!/ST4 ALLOCATE(MPARS(IMOD)%SRCPS%SATWEIGHTS(MTH,2*SDSNTH+1))
!/ST4 ALLOCATE(MPARS(IMOD)%SRCPS%CUMULW(MK,MTH,MK,MTH))
!
      SGRDS(IMOD)%SINIT  = .TRUE.
!
!/T      WRITE (NDST,9001)
!
! -------------------------------------------------------------------- /
! 3.  Point to allocated arrays
!
      CALL W3SETG ( IMOD, NDSE, NDST )
!
!/T      WRITE (NDST,9002)
!
! -------------------------------------------------------------------- /
! 4.  Update counters in grid
!
      NK     = MK
      NK2    = MK + 2
      NTH    = MTH
      NSPEC  = MK * MTH
!
!/T      WRITE (NDST,9003)
!
      RETURN
!
! Formats
!
 1001 FORMAT (/' *** ERROR W3DIMS : GRIDS NOT INITIALIZED *** '/      &
               '                    RUN W3NMOD FIRST '/)
 1002 FORMAT (/' *** ERROR W3DIMS : ILLEGAL MODEL NUMBER *** '/       &
               '                    IMOD   = ',I10/                   &
               '                    NAUXGR = ',I10/                   &
               '                    NGRIDS = ',I10/)
 1003 FORMAT (/' *** ERROR W3DIMS : ILLEGAL GRID DIMENSION(S) *** '/  &
               '                    INPUT = ',4I10/)
 1004 FORMAT (/' *** ERROR W3DIMS : ARRAY(S) ALREADY ALLOCATED *** ')
!
!/T 9000 FORMAT (' TEST W3DIMS : MODEL ',I4,' DIM. AT ',3I5,I7)
!/T 9001 FORMAT (' TEST W3DIMS : ARRAYS ALLOCATED')
!/T 9002 FORMAT (' TEST W3DIMS : POINTERS RESET')
!/T 9003 FORMAT (' TEST W3DIMS : DIMENSIONS STORED')
!/
!/ End of W3DIMS ----------------------------------------------------- /
!/
      END SUBROUTINE W3DIMS
!/ ------------------------------------------------------------------- /
      SUBROUTINE W3SETG ( IMOD, NDSE, NDST )
!/
!/                  +-----------------------------------+
!/                  | WAVEWATCH III           NOAA/NCEP |
!/                  |           H. L. Tolman            |
!/                  !           J. H. Alves             !
!/                  |            F. Ardhuin             |
!/                  |                        FORTRAN 90 |
!/                  | Last update :         30-Oct-2009 |
!/                  +-----------------------------------+
!/
!/    24-Jun-2005 : Origination.                        ( version 3.07 )
!/    09-Nov-2005 : Remove soft boundary options.       ( version 3.08 )
!/    23-Jun-2006 : Add data for W3SLN1.                ( version 3.09 )
!/    18-Jul-2006 : Add input grids.                    ( version 3.10 )
!/    05-Oct-2006 : Add filter to array pointers.       ( version 3.10 )
!/    02-Feb-2007 : Add FLAGST.                         ( version 3.10 )
!/    14-Apr-2007 : Add Miche style limiter.            ( version 3.11 )
!/                  ( J. H. Alves )
!/    25-Apr-2007 : Adding Battjes-Janssen Sdb.         ( version 3.11 )
!/                  ( J. H. Alves )
!/    18-Sep-2007 : Adding WAM4 source terms.           ( version 3.13 )
!/                  ( F. Ardhuin )
!/    27-Jun-2008 : Expand WAM4 variants namelist       ( version 3.14 )
!/                  ( F. Ardhuin )
!/    30-Oct-2009 : Implement run-time grid selection.  ( version 3.14 )
!/                  (W. E. Rogers & T. J. Campbell, NRL)
!/    30-Oct-2009 : Implement curvilinear grid type.    ( version 3.14 )
!/                  (W. E. Rogers & T. J. Campbell, NRL)
!/
!  1. Purpose :
!
!     Select one of the WAVEWATCH III grids / models.
!
!  2. Method :
!
!     Point pointers to the proper variables in the proper element of
!     the GRIDS array.
!
!  3. Parameters :
!
!     Parameter list
!     ----------------------------------------------------------------
!       IMOD    Int.   I   Model number to point to.
!       NDSE    Int.   I   Error output unit number.
!       NDST    Int.   I   Test output unit number.
!     ----------------------------------------------------------------
!
!  4. Subroutines used :
!
!     See module documentation.
!
!  5. Called by :
!
!     Many subroutines in eth WAVEWATCH system.
!
!  6. Error messages :
!
!     Checks on parameter list IMOD.
!
!  7. Remarks :
!
!  8. Structure :
!
!  9. Switches :
!
!     !/PRn  Select propagation scheme
!
!     !/STn  Select source terms
!     !/NLn
!     !/BTn
!
!     !/S    Enable subroutine tracing.
!     !/T    Enable test output
!
! 10. Source code :
!
!/ ------------------------------------------------------------------- /
      USE W3SERVMD, ONLY: EXTCDE
!/S      USE W3SERVMD, ONLY: STRACE
!
      IMPLICIT NONE
!
!/
!/ ------------------------------------------------------------------- /
!/ Parameter list
!/
      INTEGER, INTENT(IN)     :: IMOD, NDSE, NDST
!/
!/ ------------------------------------------------------------------- /
!/ Local parameters
!/
!/S      INTEGER, SAVE           :: IENT = 0
!/
!/S      CALL STRACE (IENT, 'W3SETG')
!
! -------------------------------------------------------------------- /
! 1.  Test input and module status
!
      IF ( NGRIDS .EQ. -1 ) THEN
          WRITE (NDSE,1001)
          CALL EXTCDE (1)
        END IF
!
      IF ( IMOD.LT.-NAUXGR .OR. IMOD.GT.NGRIDS ) THEN
          WRITE (NDSE,1002) IMOD, -NAUXGR, NGRIDS
          CALL EXTCDE (2)
        END IF
!
!/T      WRITE (NDST,9000) IMOD
!
! -------------------------------------------------------------------- /
! 2.  Set model numbers
!
      IGRID  = IMOD
      ISGRD  = IMOD
      IPARS  = IMOD
!
! -------------------------------------------------------------------- /
! 3.  Set pointers in structure GRID
!
      GTYPE  => GRIDS(IMOD)%GTYPE
      GLOBAL => GRIDS(IMOD)%GLOBAL
!
      NX     => GRIDS(IMOD)%NX
      NY     => GRIDS(IMOD)%NY
      NSEA   => GRIDS(IMOD)%NSEA
      NSEAL  => GRIDS(IMOD)%NSEAL
      TRFLAG => GRIDS(IMOD)%TRFLAG
      P2MSF  => GRIDS(IMOD)%P2MSF
      US3DF  => GRIDS(IMOD)%US3DF
      REFLC  => GRIDS(IMOD)%REFLC
      REFLD  => GRIDS(IMOD)%REFLD
      CONTOUR    => GRIDS(IMOD)%CONTOUR
!/COU     COUG_2D     => GRIDS(IMOD)%COUG_2D
!/COU     COUG_RAD3D  => GRIDS(IMOD)%COUG_RAD3D
!/COU     COUG_US3D   => GRIDS(IMOD)%COUG_US3D
      FLICES => GRIDS(IMOD)%FLICES
      RREF   => GRIDS(IMOD)%RREF
      REFPARS=> GRIDS(IMOD)%REFPARS
      SX     => GRIDS(IMOD)%SX
      SY     => GRIDS(IMOD)%SY
      X0     => GRIDS(IMOD)%X0
      Y0     => GRIDS(IMOD)%Y0
!
      DTCFL  => GRIDS(IMOD)%DTCFL
      DTCFLI => GRIDS(IMOD)%DTCFLI
      DTMAX  => GRIDS(IMOD)%DTMAX
      DTMIN  => GRIDS(IMOD)%DTMIN
      DMIN   => GRIDS(IMOD)%DMIN
      CTMAX  => GRIDS(IMOD)%CTMAX
      FICE0  => GRIDS(IMOD)%FICE0
      FICEN  => GRIDS(IMOD)%FICEN
      PFMOVE => GRIDS(IMOD)%PFMOVE
!
      GINIT  => GRIDS(IMOD)%GINIT
      GUGINIT  => GRIDS(IMOD)%GUGINIT      
      GLOBAL => GRIDS(IMOD)%GLOBAL
      FLDRY  => GRIDS(IMOD)%FLDRY
      FLCX   => GRIDS(IMOD)%FLCX
      FLCY   => GRIDS(IMOD)%FLCY
      FLCTH  => GRIDS(IMOD)%FLCTH
      FLCK   => GRIDS(IMOD)%FLCK
      FLSOU  => GRIDS(IMOD)%FLSOU
!
      GNAME  => GRIDS(IMOD)%GNAME
      FILEXT => GRIDS(IMOD)%FILEXT
      XYB    => GRIDS(IMOD)%XYB
      TRIGP  => GRIDS(IMOD)%TRIGP
      NTRI     => GRIDS(IMOD)%NTRI
      COUNTRI     => GRIDS(IMOD)%COUNTRI
      SI     => GRIDS(IMOD)%SI
      COUNTOT    => GRIDS(IMOD)%COUNTOT
      IEN     => GRIDS(IMOD)%IEN
      LEN     => GRIDS(IMOD)%LEN 
      ANGLE     => GRIDS(IMOD)%ANGLE 
      CCON     => GRIDS(IMOD)%CCON
      COUNTCON     => GRIDS(IMOD)%COUNTCON
      IE_CELL     => GRIDS(IMOD)%IE_CELL
      POS_CELL     => GRIDS(IMOD)%POS_CELL
      IOBP     => GRIDS(IMOD)%IOBP
      IAA      => GRIDS(IMOD)%IAA
      JAA      => GRIDS(IMOD)%JAA
      POSI     => GRIDS(IMOD)%POSI
      NNZ      => GRIDS(IMOD)%NNZ
      IOBPD     => GRIDS(IMOD)%IOBPD
      TRIA     => GRIDS(IMOD)%TRIA
      TRIA03     => GRIDS(IMOD)%TRIA03
      CROSSDIFF => GRIDS(IMOD)%CROSSDIFF
      MAXX     => GRIDS(IMOD)%MAXX
      MAXY     => GRIDS(IMOD)%MAXY
      DXYMAX   => GRIDS(IMOD)%DXYMAX      

!
      IF ( GINIT ) THEN
!
          MAPSTA => GRIDS(IMOD)%MAPSTA
          MAPST2 => GRIDS(IMOD)%MAPST2
          MAPFS  => GRIDS(IMOD)%MAPFS
          MAPSF  => GRIDS(IMOD)%MAPSF
          FLAGST => GRIDS(IMOD)%FLAGST
!
          ZB     => GRIDS(IMOD)%ZB
          CLATS  => GRIDS(IMOD)%CLATS
          CLATIS => GRIDS(IMOD)%CLATIS
          CTHG0S => GRIDS(IMOD)%CTHG0S
          TRNX   => GRIDS(IMOD)%TRNX
          TRNY   => GRIDS(IMOD)%TRNY
!
          XGRD   => GRIDS(IMOD)%XGRD
          YGRD   => GRIDS(IMOD)%YGRD
          DXDP   => GRIDS(IMOD)%DXDP
          DXDQ   => GRIDS(IMOD)%DXDQ
          DYDP   => GRIDS(IMOD)%DYDP
          DYDQ   => GRIDS(IMOD)%DYDQ
          DPDX   => GRIDS(IMOD)%DPDX
          DPDY   => GRIDS(IMOD)%DPDY
          DQDX   => GRIDS(IMOD)%DQDX
          DQDY   => GRIDS(IMOD)%DQDY
          GSQRT  => GRIDS(IMOD)%GSQRT
          HPFAC  => GRIDS(IMOD)%HPFAC
          HQFAC  => GRIDS(IMOD)%HQFAC
!
          GSU  => GRIDS(IMOD)%GSU
!
        END IF
!
! -------------------------------------------------------------------- /
! 4.  Set pointers in structure SGRD
!
      NK     => SGRDS(IMOD)%NK
      NK2    => SGRDS(IMOD)%NK2
      NTH    => SGRDS(IMOD)%NTH
      NSPEC  => SGRDS(IMOD)%NSPEC
!
      DTH    => SGRDS(IMOD)%DTH
      XFR    => SGRDS(IMOD)%XFR
      FR1    => SGRDS(IMOD)%FR1
      FTE    => SGRDS(IMOD)%FTE
      FTF    => SGRDS(IMOD)%FTF
      FTWN   => SGRDS(IMOD)%FTWN
      FTTR   => SGRDS(IMOD)%FTTR
      FTWL   => SGRDS(IMOD)%FTWL
      FACTI1 => SGRDS(IMOD)%FACTI1
      FACTI2 => SGRDS(IMOD)%FACTI2
      FACHFA => SGRDS(IMOD)%FACHFA
      FACHFE => SGRDS(IMOD)%FACHFE
!
      SINIT  => SGRDS(IMOD)%SINIT
!
      IF ( SINIT ) THEN
!
          MAPWN  => SGRDS(IMOD)%MAPWN
          MAPTH  => SGRDS(IMOD)%MAPTH
!
          TH     => SGRDS(IMOD)%TH
          ESIN   => SGRDS(IMOD)%ESIN
          ECOS   => SGRDS(IMOD)%ECOS
          ES2    => SGRDS(IMOD)%ES2
          ESC    => SGRDS(IMOD)%ESC
          EC2    => SGRDS(IMOD)%EC2
          SIG    => SGRDS(IMOD)%SIG
          SIG2   => SGRDS(IMOD)%SIG2
          DSIP   => SGRDS(IMOD)%DSIP
          DSII   => SGRDS(IMOD)%DSII
          DDEN   => SGRDS(IMOD)%DDEN
          DDEN2  => SGRDS(IMOD)%DDEN2
!
        END IF
!
! -------------------------------------------------------------------- /
! 5.  Set pointers in structure MPAR
!
      PINIT  => MPARS(IMOD)%PINIT
!
!     Structure NPARS
!
      FACP   => MPARS(IMOD)%NPARS%FACP
      XREL   => MPARS(IMOD)%NPARS%XREL
      XFLT   => MPARS(IMOD)%NPARS%XFLT
      FXFM   => MPARS(IMOD)%NPARS%FXFM
      FXPM   => MPARS(IMOD)%NPARS%FXPM
      XFT    => MPARS(IMOD)%NPARS%XFT
      XFC    => MPARS(IMOD)%NPARS%XFC
      FACSD  => MPARS(IMOD)%NPARS%FACSD
      FHMAX  => MPARS(IMOD)%NPARS%FHMAX
!/RWND    RWINDC => MPARS(IMOD)%NPARS%RWINDC
!
!     Structure PROPS
!
!/PR2      DTME   => MPARS(IMOD)%PROPS%DTME
!/PR2      CLATMN => MPARS(IMOD)%PROPS%CLATMN
!/PR3      WDCG   => MPARS(IMOD)%PROPS%WDCG
!/PR3      WDTH   => MPARS(IMOD)%PROPS%WDTH
!
!     Structure SFLPS
!
!/FLX2      NITTIN => MPARS(IMOD)%SFLPS%NITTIN
!/FLX2      CINXSI => MPARS(IMOD)%SFLPS%CINXSI
!/FLX3      NITTIN => MPARS(IMOD)%SFLPS%NITTIN
!/FLX3      CAP_ID => MPARS(IMOD)%SFLPS%CAP_ID
!/FLX3      CINXSI => MPARS(IMOD)%SFLPS%CINXSI
!/FLX3      CD_MAX => MPARS(IMOD)%SFLPS%CD_MAX
!
!     Structure SLNPS
!
!/LN1      SLNC1  => MPARS(IMOD)%SLNPS%SLNC1
!/LN1      FSPM   => MPARS(IMOD)%SLNPS%FSPM
!/LN1      FSHF   => MPARS(IMOD)%SLNPS%FSHF
!
!     Structure SRCPS
!
      WWNMEANPTAIL=> MPARS(IMOD)%SRCPS%WWNMEANPTAIL
      SSTXFTFTAIL => MPARS(IMOD)%SRCPS%SSTXFTFTAIL
!/ST1      SINC1  => MPARS(IMOD)%SRCPS%SINC1
!/ST1      SDSC1  => MPARS(IMOD)%SRCPS%SDSC1
!/ST2      ZWIND  => MPARS(IMOD)%SRCPS%ZWIND
!/ST2      FSWELL => MPARS(IMOD)%SRCPS%FSWELL
!/ST2      SHSTAB => MPARS(IMOD)%SRCPS%SHSTAB
!/ST2      OFSTAB => MPARS(IMOD)%SRCPS%OFSTAB
!/ST2      CCNG   => MPARS(IMOD)%SRCPS%CCNG
!/ST2      CCPS   => MPARS(IMOD)%SRCPS%CCPS
!/ST2      FFNG   => MPARS(IMOD)%SRCPS%FFNG
!/ST2      FFPS   => MPARS(IMOD)%SRCPS%FFPS
!/ST2      CDSA0  => MPARS(IMOD)%SRCPS%CDSA0
!/ST2      CDSA1  => MPARS(IMOD)%SRCPS%CDSA1
!/ST2      CDSA2  => MPARS(IMOD)%SRCPS%CDSA2
!/ST2      SDSALN => MPARS(IMOD)%SRCPS%SDSALN
!/ST2      CDSB0  => MPARS(IMOD)%SRCPS%CDSB0
!/ST2      CDSB1  => MPARS(IMOD)%SRCPS%CDSB1
!/ST2      CDSB2  => MPARS(IMOD)%SRCPS%CDSB2
!/ST2      CDSB3  => MPARS(IMOD)%SRCPS%CDSB3
!/ST2      FPIMIN => MPARS(IMOD)%SRCPS%FPIMIN
!/ST2      XFH    => MPARS(IMOD)%SRCPS%XFH
!/ST2      XF1    => MPARS(IMOD)%SRCPS%XF1
!/ST2      XF2    => MPARS(IMOD)%SRCPS%XF2
!
!/ST3      ZZWND  => MPARS(IMOD)%SRCPS%ZZWND
!/ST3      AALPHA => MPARS(IMOD)%SRCPS%AALPHA
!/ST3      BBETA  => MPARS(IMOD)%SRCPS%BBETA
!/ST3      SSINTHP  => MPARS(IMOD)%SRCPS%SSINTHP
!/ST3      ZZ0MAX  => MPARS(IMOD)%SRCPS%ZZ0MAX
!/ST3      ZZ0RAT  => MPARS(IMOD)%SRCPS%ZZ0RAT
!/ST3      ZZALP  => MPARS(IMOD)%SRCPS%ZZALP
!/ST3      TTAUWSHELTER  => MPARS(IMOD)%SRCPS%TTAUWSHELTER
!/ST3      SSWELLF  => MPARS(IMOD)%SRCPS%SSWELLF
!/ST3      SSDSC1 => MPARS(IMOD)%SRCPS%SSDSC1
!/ST3      WWNMEANP => MPARS(IMOD)%SRCPS%WWNMEANP
!/ST3      FFXFM => MPARS(IMOD)%SRCPS%FFXFM
!/ST3      FFXPM => MPARS(IMOD)%SRCPS%FFXPM
!/ST3      DDELTA1 => MPARS(IMOD)%SRCPS%DDELTA1
!/ST3      DDELTA2 => MPARS(IMOD)%SRCPS%DDELTA2
!/ST3      SSTXFTF => MPARS(IMOD)%SRCPS%SSTXFTF
!/ST3      SSTXFTWN => MPARS(IMOD)%SRCPS%SSTXFTWN
!
!/ST4      ZZWND  => MPARS(IMOD)%SRCPS%ZZWND
!/ST4      AALPHA => MPARS(IMOD)%SRCPS%AALPHA
!/ST4      BBETA  => MPARS(IMOD)%SRCPS%BBETA
!/ST4      SSINTHP  => MPARS(IMOD)%SRCPS%SSINTHP
!/ST4      ZZ0MAX  => MPARS(IMOD)%SRCPS%ZZ0MAX
!/ST4      ZZ0RAT  => MPARS(IMOD)%SRCPS%ZZ0RAT
!/ST4      ZZALP  => MPARS(IMOD)%SRCPS%ZZALP
!/ST4      TTAUWSHELTER  => MPARS(IMOD)%SRCPS%TTAUWSHELTER
!/ST4      SSWELLFPAR  => MPARS(IMOD)%SRCPS%SSWELLFPAR
!/ST4      SSWELLF  => MPARS(IMOD)%SRCPS%SSWELLF
!/ST4      SSDSC1 => MPARS(IMOD)%SRCPS%SSDSC1
!/ST4      SSDSC2 => MPARS(IMOD)%SRCPS%SSDSC2
!/ST4      SSDSC3 => MPARS(IMOD)%SRCPS%SSDSC3
!/ST4      SSDSC4 => MPARS(IMOD)%SRCPS%SSDSC4
!/ST4      SSDSC5 => MPARS(IMOD)%SRCPS%SSDSC5
!/ST4      SSDSC6 => MPARS(IMOD)%SRCPS%SSDSC6
!/ST4      SSDSBR => MPARS(IMOD)%SRCPS%SSDSBR
!/ST4      SSDSBR2 => MPARS(IMOD)%SRCPS%SSDSBR2
!/ST4      SSDSBRF1 => MPARS(IMOD)%SRCPS%SSDSBRF1
!/ST4      SSDSBRF2 => MPARS(IMOD)%SRCPS%SSDSBRF2
!/ST4      SSDSBRFDF  => MPARS(IMOD)%SRCPS%SSDSBRFDF
!/ST4      SSDSBM => MPARS(IMOD)%SRCPS%SSDSBM
!/ST4      SSDSBCK => MPARS(IMOD)%SRCPS%SSDSBCK
!/ST4      SSDSABK => MPARS(IMOD)%SRCPS%SSDSABK
!/ST4      SSDSPBK => MPARS(IMOD)%SRCPS%SSDSPBK
!/ST4      SSDSHCK => MPARS(IMOD)%SRCPS%SSDSHCK
!/ST4      SSDSBINT => MPARS(IMOD)%SRCPS%SSDSBINT
!/ST4      SSDSP  => MPARS(IMOD)%SRCPS%SSDSP
!/ST4      WWNMEANP => MPARS(IMOD)%SRCPS%WWNMEANP
!/ST4      FFXFM => MPARS(IMOD)%SRCPS%FFXFM
!/ST4      FFXPM => MPARS(IMOD)%SRCPS%FFXPM
!/ST4      SSDSDTH => MPARS(IMOD)%SRCPS%SSDSDTH
!/ST4      SSTXFTF => MPARS(IMOD)%SRCPS%SSTXFTF
!/ST4      SSTXFTWN => MPARS(IMOD)%SRCPS%SSTXFTWN
!/ST4      SSDSCOS => MPARS(IMOD)%SRCPS%SSDSCOS
!/ST4      SSDSISO => MPARS(IMOD)%SRCPS%SSDSISO
!/ST4      IKTAB   => MPARS(IMOD)%SRCPS%IKTAB  
!/ST4      DCKI    => MPARS(IMOD)%SRCPS%DCKI  
!/ST4      CUMULW   => MPARS(IMOD)%SRCPS%CUMULW
!/ST4      SATINDICES    => MPARS(IMOD)%SRCPS%SATINDICES
!/ST4      SATWEIGHTS   => MPARS(IMOD)%SRCPS%SATWEIGHTS
!
!     Structure SRNLS
!
!/NL1      SNLC1  => MPARS(IMOD)%SNLPS%SNLC1
!/NL1      LAM    => MPARS(IMOD)%SNLPS%LAM
!/NL1      KDCON  => MPARS(IMOD)%SNLPS%KDCON
!/NL1      KDMN   => MPARS(IMOD)%SNLPS%KDMN
!/NL1      SNLS1  => MPARS(IMOD)%SNLPS%SNLS1
!/NL1      SNLS2  => MPARS(IMOD)%SNLPS%SNLS2
!/NL1      SNLS3  => MPARS(IMOD)%SNLPS%SNLS3
!/NL2      IQTPE  => MPARS(IMOD)%SNLPS%IQTPE
!/NL2      NDPTHS => MPARS(IMOD)%SNLPS%NDPTHS
!/NL2      NLTAIL => MPARS(IMOD)%SNLPS%NLTAIL
!/NL2      IF ( NDPTHS .NE. 0 ) DPTHNL => MPARS(IMOD)%SNLPS%DPTHNL
!
!     Structure SBTPS
!
!/BT1      SBTC1  => MPARS(IMOD)%SBTPS%SBTC1
!
!     Structure SDBPS
!
!/DB1      SDBC1  => MPARS(IMOD)%SDBPS%SDBC1
!/DB1      SDBC2  => MPARS(IMOD)%SDBPS%SDBC2
!/DB1      FDONLY => MPARS(IMOD)%SDBPS%FDONLY
!    Structure SCHM
       FSN => MPARS(IMOD)%SCHMS%FSN
       FSPSI => MPARS(IMOD)%SCHMS%FSPSI
       FSFCT => MPARS(IMOD)%SCHMS%FSFCT
       FSNIMP => MPARS(IMOD)%SCHMS%FSNIMP

      RETURN
!
! Formats
!
 1001 FORMAT (/' *** ERROR W3SETG : GRIDS NOT INITIALIZED *** '/      &
               '                    RUN W3NMOD FIRST '/)
 1002 FORMAT (/' *** ERROR W3SETG : ILLEGAL MODEL NUMBER *** '/       &
               '                    IMOD   = ',I10/                   &
               '                    NAUXGR = ',I10/                   &
               '                    NGRIDS = ',I10/)
!
!/T 9000 FORMAT (' TEST W3SETG : GRID/MODEL ',I4,' SELECTED')
!/
!/ End of W3SETG ----------------------------------------------------- /
!/
      END SUBROUTINE W3SETG
!/ ------------------------------------------------------------------- /
      SUBROUTINE W3GNTX ( IMOD, NDSE, NDST )
!/
!/                  +-----------------------------------+
!/                  | WAVEWATCH-III           NOAA/NCEP |
!/                  |           T. J. Campbell          |
!/                  |                        FORTRAN 90 |
!/                  | Last update :         30-Oct-2009 |
!/                  +-----------------------------------+
!/
!/    30-Oct-2009 : Origination.                        ( version 3.13 )
!/
!  1. Purpose :
!
!     Construct required spatial grid quantities for curvilinear grids.
!
!  2. Method :
!
!  3. Parameters :
!
!     Parameter list
!     ----------------------------------------------------------------
!       IMOD    Int.   I   Model number to point to.
!       NDSE    Int.   I   Error output unit number.
!     ----------------------------------------------------------------
!
!  4. Subroutines used :
!
!     See module documentation.
!
!  5. Called by :
!
!     Any program that uses this grid structure.
!
!  6. Error messages :
!
!     - Check on previous initialization of grids.
!
!  7. Remarks :
!
!  8. Structure :
!
!  9. Switches :
!
!     !/S    Enable subroutine tracing.
!     !/T    Enable test output
!
! 10. Source code :
!
!/ ------------------------------------------------------------------- /
      USE W3SERVMD, ONLY: EXTCDE
!/S      USE W3SERVMD, ONLY: STRACE
!
      IMPLICIT NONE
!/
!/ ------------------------------------------------------------------- /
!/ Parameter list
!/
      INTEGER, INTENT(IN)     :: IMOD, NDSE, NDST
!/
!/ ------------------------------------------------------------------- /
!/ Local parameters
!/
      REAL(8), PARAMETER :: PI = 3.14159265358979323846D0
      REAL(8), PARAMETER :: D360 = 360D0
      REAL(8), PARAMETER :: D180 = 180D0
      REAL(8), PARAMETER :: D2R = PI/D180
      REAL(8), PARAMETER :: R2D = 1D0/D2R
      REAL(8), PARAMETER :: HALF = 0.5D0
      REAL(8)            :: DXCIRC(3)
      INTEGER            :: IX, IY
!/T      REAL               :: ANGPQ(NY,NX)
!     REAL               :: UVECPX(NY,NX), UVECPY(NY,NX)
!     REAL               :: UVECQX(NY,NX), UVECQY(NY,NX)
!/S      INTEGER, SAVE      :: IENT = 0
!/
!/S      CALL STRACE (IENT, 'W3GNTX')
!
! -------------------------------------------------------------------- /
! 1.  Test input and module status
!
      IF ( NGRIDS .EQ. -1 ) THEN
          WRITE (NDSE,1001)
          CALL EXTCDE (1)
        END IF
!
      IF ( IMOD.LT.-NAUXGR .OR. IMOD.GT.NGRIDS ) THEN
          WRITE (NDSE,1002) IMOD, -NAUXGR, NGRIDS
          CALL EXTCDE (2)
        END IF
!
!/T      WRITE (NDST,9000) IMOD
!
! -------------------------------------------------------------------- /
! 2.  Create grid search utility object
!
      GRIDS(IMOD)%GSU = W3GSUC( .FALSE., FLAGLL, GRIDS(IMOD)%GLOBAL, &
                                GRIDS(IMOD)%NX, GRIDS(IMOD)%NY, &
                                GRIDS(IMOD)%XGRD, GRIDS(IMOD)%YGRD )
!/T      CALL W3GSUP(GRIDS(IMOD)%GSU, NDST)
!
!/T      WRITE (NDST,9001)
!
! -------------------------------------------------------------------- /
! 3.  Reset grid pointers
!
      CALL W3SETG ( IMOD, NDSE, NDST )
!
!/T      WRITE (NDST,9002)
!
! -------------------------------------------------------------------- /
! 4.  Construct curvilinear grid derivatives and metric
!
!  x = x(p,q);  y = y(p,q);  dp = dq = 1;
!
!  When using spherical coordinates x = longitude and y = latitude.
!  Also, the derivatives with respect to p are adjusted by the spherical
!  coordinate metric.  In other words (with radius = 1),
!
!      dx/dp <= cos(y)*(dx/dp)  and  dy/dp <= cos(y)*(dy/dp).
!
!  The 2D curvilinear coordinates metric is
!
!      g_pq = (dx/dp)*(dx/dq) + (dy/dp)*(dy/dq),
!      g_pp = (dx/dp)*(dx/dp) + (dy/dp)*(dy/dp),
!      g_qq = (dx/dq)*(dx/dq) + (dy/dq)*(dy/dq).
!
!  Derivatives are computed with central differences for all non-boundary
!  cells.  One-sided differences are used at boundary cells.  If the grid
!  is a global grid, then the i-differences at the i=1 and i=nx boundary
!  cells are computed with central differences using the wrapped cell
!  x-coordinates.
      !
      !  Compute dx/dp & dy/dp
      IX = 1
      IF(FLAGLL)THEN
         DO IY=1,NY
            DXCIRC(1)=       ( XGRD(IY ,IX+1) - XGRD(IY ,IX  ) )-D360
            DXCIRC(2)=       ( XGRD(IY ,IX+1) - XGRD(IY ,IX  ) )
            DXCIRC(3)=       ( XGRD(IY ,IX+1) - XGRD(IY ,IX  ) )+D360
            DXDP(IY,IX)=DXCIRC(MINLOC(ABS(DXCIRC),1))
         END DO
      ELSE
         DXDP(: ,IX) =        ( XGRD(: ,IX+1) - XGRD(: ,IX  ) )
      ENDIF
      DYDP(: ,IX) =        ( YGRD(: ,IX+1) - YGRD(: ,IX  ) )

      IX = NX
      IF(FLAGLL)THEN
         DO IY=1,NY
            DXCIRC(1)=       ( XGRD(IY ,IX  ) - XGRD(IY ,IX-1) )-D360
            DXCIRC(2)=       ( XGRD(IY ,IX  ) - XGRD(IY ,IX-1) )
            DXCIRC(3)=       ( XGRD(IY ,IX  ) - XGRD(IY ,IX-1) )+D360
            DXDP(IY,IX)=DXCIRC(MINLOC(ABS(DXCIRC),1))
         END DO
      ELSE
         DXDP(: ,IX) =        ( XGRD(: ,IX  ) - XGRD(: ,IX-1) )
      ENDIF
      DYDP(: ,IX) =        ( YGRD(: ,IX  ) - YGRD(: ,IX-1) )

      DO IX=2, NX-1

         IF(FLAGLL)THEN
            DO IY=1,NY
               DXCIRC(1)= ( XGRD(IY ,IX+1) - XGRD(IY ,IX-1) )-D360
               DXCIRC(2)= ( XGRD(IY ,IX+1) - XGRD(IY ,IX-1) )
               DXCIRC(3)= ( XGRD(IY ,IX+1) - XGRD(IY ,IX-1) )+D360
               DXDP(IY,IX)=HALF * DXCIRC(MINLOC(ABS(DXCIRC),1))
            END DO
         ELSE
            DXDP(: ,IX)   = HALF * ( XGRD(: ,IX+1) - XGRD(: ,IX-1) )
         ENDIF

         DYDP(: ,IX)   = HALF * ( YGRD(: ,IX+1) - YGRD(: ,IX-1) )

      END DO
      !
      !  Compute dx/dq & dy/dq

      IY = 1
      IF(FLAGLL)THEN
         DO IX=1,NX
            DXCIRC(1)=       ( XGRD(IY+1,IX) - XGRD(IY  ,IX) )-D360
            DXCIRC(2)=       ( XGRD(IY+1,IX) - XGRD(IY  ,IX) )
            DXCIRC(3)=       ( XGRD(IY+1,IX) - XGRD(IY  ,IX) )+D360
            DXDQ(IY,IX)=DXCIRC(MINLOC(ABS(DXCIRC),1))
         END DO
      ELSE
         DXDQ(IY,: )   =        ( XGRD(IY+1,: ) - XGRD(IY  ,: ) )
      ENDIF
      DYDQ(IY,: )   =        ( YGRD(IY+1,: ) - YGRD(IY  ,: ) )

      IY = NY
      IF(FLAGLL)THEN
         DO IX=1,NX
            DXCIRC(1)=       ( XGRD(IY  ,IX) - XGRD(IY-1,IX) )-D360
            DXCIRC(2)=       ( XGRD(IY  ,IX) - XGRD(IY-1,IX) )
            DXCIRC(3)=       ( XGRD(IY  ,IX) - XGRD(IY-1,IX) )+D360
            DXDQ(IY,IX)=DXCIRC(MINLOC(ABS(DXCIRC),1))
         END DO
      ELSE
         DXDQ(IY,: )   =        ( XGRD(IY  ,: ) - XGRD(IY-1,: ) )
      ENDIF
      DYDQ(IY,: )   =        ( YGRD(IY  ,: ) - YGRD(IY-1,: ) )

      DO IY=2, NY-1
         IF(FLAGLL)THEN
            DO IX=1,NX
               DXCIRC(1)= ( XGRD(IY+1,IX) - XGRD(IY-1,IX) )-D360
               DXCIRC(2)= ( XGRD(IY+1,IX) - XGRD(IY-1,IX) )
               DXCIRC(3)= ( XGRD(IY+1,IX) - XGRD(IY-1,IX) )+D360
               DXDQ(IY,IX)=HALF * DXCIRC(MINLOC(ABS(DXCIRC),1))
            END DO
         ELSE
            DXDQ(IY,: ) = HALF * ( XGRD(IY+1,: ) - XGRD(IY-1,: ) )
         ENDIF
         DYDQ(IY,: ) = HALF * ( YGRD(IY+1,: ) - YGRD(IY-1,: ) )
      END DO

!
!  Convert from degrees to radians
!  Account for spherical metric if lat/lon grid (only if the metric
!  is to be w.r.t. the Cartesian space)
!     IF ( FLAGLL ) THEN
!         DXDP = DXDP * D2R !* COS(YGRD*D2R)
!         DXDQ = DXDQ * D2R !* COS(YGRD*D2R)
!         DYDP = DYDP * D2R
!         DYDQ = DYDQ * D2R
!       END IF
!
!  HPFAC: h_p = sqrt(g_pp) = sqrt( (dx/dp)^2 + (dy/dp)^2 )
      HPFAC = SQRT( DXDP**2 + DYDP**2 )
!/T      WRITE(NDST,'(A,2E14.6)')'HPFAC MIN/MAX:',MINVAL(HPFAC),MAXVAL(HPFAC)
!
!  HQFAC: h_q = sqrt(g_qq) = sqrt( (dx/dq)^2 + (dy/dq)^2 )
      HQFAC = SQRT( DXDQ**2 + DYDQ**2 )
!/T      WRITE(NDST,'(A,2E14.6)')'HQFAC MIN/MAX:',MINVAL(HQFAC),MAXVAL(HQFAC)
!
!  GSQRT: sqrt(g) = (dx/dp)(dy/dq) - (dx/dq)(dy/dp)
      GSQRT = DXDP*DYDQ - DXDQ*DYDP
!/T      WRITE(NDST,'(A,2E14.6)')'GSQRT MIN/MAX:',MINVAL(GSQRT),MAXVAL(GSQRT)
!
!  Compute curvilinear derivatives
!  DPDX: dp/dx =  (1/sqrt(g))*(dy/dq)
!  DPDY: dp/dy = -(1/sqrt(g))*(dx/dq)
!  DQDX: dq/dx = -(1/sqrt(g))*(dy/dp)
!  DQDY: dq/dy =  (1/sqrt(g))*(dx/dp)
      DPDX =  DYDQ / GSQRT
      DPDY = -DXDQ / GSQRT
      DQDX = -DYDP / GSQRT
      DQDY =  DXDP / GSQRT
!/T      WRITE(NDST,'(A,2E14.6)')'DPDX MIN/MAX:',MINVAL(DPDX),MAXVAL(DPDX)
!/T      WRITE(NDST,'(A,2E14.6)')'DPDY MIN/MAX:',MINVAL(DPDY),MAXVAL(DPDY)
!/T      WRITE(NDST,'(A,2E14.6)')'DQDX MIN/MAX:',MINVAL(DQDX),MAXVAL(DQDX)
!/T      WRITE(NDST,'(A,2E14.6)')'DQDY MIN/MAX:',MINVAL(DQDY),MAXVAL(DQDY)
!
!  Check orthogonality of grid by computing angle between the
!  curvilinear coordinate unit vectors.
!    cos(a) = uvec_p \dot uvec_q = g_pq/sqrt(g_pp*g_qq)
!/T      ANGPQ = R2D * ACOS( ( DXDP*DXDQ + DYDP*DYDQ ) / ( HPFAC*HQFAC ) )
!/T      WRITE(NDST,'(A,2E14.6)')'ANGPQ MIN/MAX:',MINVAL(ANGPQ),MAXVAL(ANGPQ)
!
!  Compute cell unit vectors
!  uvec_p = (1/h_p)*(dx/dp)*uvec_x + (1/h_p)*(dy/dp)*uvec_y
!  uvec_q = (1/h_q)*(dx/dq)*uvec_x + (1/h_q)*(dy/dq)*uvec_y
!     UVECPX = DXDP / HPFAC
!     UVECPY = DYDP / HPFAC
!     UVECQX = DXDQ / HQFAC
!     UVECQY = DYDQ / HQFAC
!
!/T      WRITE (NDST,9003)
!
! Formats
!
 1001 FORMAT (/' *** ERROR W3GNTX : GRIDS NOT INITIALIZED *** '/      &
               '                    RUN W3NMOD FIRST '/)
 1002 FORMAT (/' *** ERROR W3GNTX : ILLEGAL MODEL NUMBER *** '/       &
               '                    IMOD   = ',I10/                   &
               '                    NAUXGR = ',I10/                   &
               '                    NGRIDS = ',I10/)
!
!/T 9000 FORMAT (' TEST W3GNTX : MODEL ',I4)
!/T 9001 FORMAT (' TEST W3GNTX : SEARCH OBJECT CREATED')
!/T 9002 FORMAT (' TEST W3GNTX : POINTERS RESET')
!/T 9003 FORMAT (' TEST W3GNTX : GRID ARRAYS CONSTRUCTED')
!/
!/ End of W3GNTX ----------------------------------------------------- /
!/
      END SUBROUTINE W3GNTX
!/ ------------------------------------------------------------------- /   
      
      SUBROUTINE W3DIMUG  ( IMOD, MTRI, MX, COUNTOTA, NNZ, NDSE, NDST )
!/
!/                  +-----------------------------------+
!/                  | WAVEWATCH-III           NOAA/NCEP |
!/                  |             F.ardhuin             |
!/                  |                        FORTRAN 90 |
!/                  | Last update :         15-Mar-2007 !
!/                  +-----------------------------------+
!/
!/    15-Mar-2007 : Origination.                        ( version 3.14 )
!/
!  1. Purpose :
!
!     Initialize an individual spatial grid at the proper dimensions.
!
!  2. Method :
!
!     Allocate directly into the structure array GRIDS. Note that
!     this cannot be done through the pointer alias!
!
!  3. Parameters :
!
!     Parameter list
!     ----------------------------------------------------------------
!       IMOD    Int.   I   Model number to point to.
!       NDSE    Int.   I   Error output unit number.
!       NDST    Int.   I   Test output unit number.
!       MX, MTRI, MSEA       Like NX, NTRI, NSEA in data structure.
!     ----------------------------------------------------------------
!
!  4. Subroutines used :
!
!       See module documentation.
!
!  5. Called by :
!
!      Name      Type  Module   Description
!     ----------------------------------------------------------------
!      W3IOGR    Subr. W3IOGRMD Model definition file IO program.
!      WW3_GRID  Prog.   N/A    Model set up program.
!     ----------------------------------------------------------------
!
!  6. Error messages :
!
!     - Check on input parameters.
!     - Check on previous allocation.
!
!  7. Remarks :
!
!     - Grid dimensions apre passed through parameter list and then 
!       locally stored to assure consistency between allocation and
!       data in structure.
!     - W3SETG needs to be called after allocation to point to 
!       proper allocated arrays.
!
!  8. Structure :
!
!     See source code.
!
!  9. Switches :
!
!     !/S    Enable subroutine tracing.
!     !/T    Enable test output
!
! 10. Source code :
!
!/ ------------------------------------------------------------------- /
      USE W3SERVMD, ONLY: EXTCDE
!/S      USE W3SERVMD, ONLY: STRACE
!
      IMPLICIT NONE
!
!/
!/ ------------------------------------------------------------------- /
!/ Parameter list
!/
      INTEGER, INTENT(IN)     :: IMOD, MTRI, MX, COUNTOTA, NNZ, NDSE, NDST
!/
!/ ------------------------------------------------------------------- /
!/ Local parameters
!/
!/S      INTEGER, SAVE           :: IENT = 0
!/
!/S      CALL STRACE (IENT, 'W3DIMUG')
!
! -------------------------------------------------------------------- /
! 1.  Test input and module status
!
      IF ( NGRIDS .EQ. -1 ) THEN
          WRITE (NDSE,1001)
          CALL EXTCDE (1)
        END IF
!
      IF ( IMOD.LT.1 .OR. IMOD.GT.NGRIDS ) THEN
          WRITE (NDSE,1002) IMOD, NGRIDS
          CALL EXTCDE (2)
        END IF
      IF ( GRIDS(IMOD)%GUGINIT ) THEN
        WRITE (NDSE,1004)
        CALL EXTCDE (4)
        END IF
!
!
!/T      WRITE (NDST,9000) IMOD, MX, MTRI
!
! -------------------------------------------------------------------- /
! 2.  Allocate arrays
!
      ALLOCATE ( GRIDS(IMOD)%TRIGP(MTRI,3),                         &
                 GRIDS(IMOD)%XYB(MX,3),                             &
                 GRIDS(IMOD)%SI(MX),                                &
                 GRIDS(IMOD)%TRIA(MTRI),                            & 
                 GRIDS(IMOD)%TRIA03(MTRI),                          &
                 GRIDS(IMOD)%CROSSDIFF(6,MTRI),                     &
                 GRIDS(IMOD)%IEN(MTRI,6),                           &
                 GRIDS(IMOD)%LEN(MTRI,3),                           &
                 GRIDS(IMOD)%ANGLE(MTRI,3),                         &
                 GRIDS(IMOD)%CCON(MX),                              &
                 GRIDS(IMOD)%COUNTCON(MX),                          &
                 GRIDS(IMOD)%IE_CELL(COUNTOTA),                     &
                 GRIDS(IMOD)%POS_CELL(COUNTOTA),                    &
                 GRIDS(IMOD)%IAA(NX+1),                    &
                 GRIDS(IMOD)%JAA(NNZ),                    &
                 GRIDS(IMOD)%POSI(3,COUNTOTA),                    &
                 GRIDS(IMOD)%IOBP(MX),                              &
                 GRIDS(IMOD)%IOBPD(NTH,MX) )
                 
                 
                                     
!/T      WRITE (NDST,9001)
!
!some segmentation troubles can appear, they are related with the allocation of 
!normal(1st dimension) and the nesting of the triangulated grid.
! -------------------------------------------------------------------- /
! 3.  Point to allocated arrays
!
      CALL W3SETG ( IMOD, NDSE, NDST )
!
!/T      WRITE (NDST,9002)
!
! -------------------------------------------------------------------- /
! 4.  Update counters in grid
!
      NTRI   = MTRI
      COUNTOT=COUNTOTA
      GUGINIT  = .FALSE.
!/T      WRITE (NDST,9003)
      RETURN
!
! Formats
!
 1001 FORMAT (/' *** ERROR W3DIMUG : GRIDS NOT INITIALIZED *** '/      &
               '                    RUN W3NMOD FIRST '/)
 1002 FORMAT (/' *** ERROR W3DIMUG : ILLEGAL MODEL NUMBER *** '/       &
               '                    IMOD   = ',I10/                   &
               '                    NGRIDS = ',I10/)
 1003 FORMAT (/' *** ERROR W3DIMUG : ILLEGAL GRID DIMENSION(S) *** '/  &
               '                    INPUT = ',I10/)
 1004 FORMAT (/' *** ERROR W3DIMUG : ARRAY(S) ALREADY ALLOCATED *** ')
!
!/T 9000 FORMAT (' TEST W3DIMUG: MODEL ',I4,' DIM. AT ',2I5,I7)
!/T 9001 FORMAT (' TEST W3DIMUG : ARRAYS ALLOCATED')
!/T 9002 FORMAT (' TEST W3DIMUG : POINTERS RESET')
!/T 9003 FORMAT (' TEST W3DIMUG : DIMENSIONS STORED')
!/
!/ End of W3DIMUG ----------------------------------------------------- /
!/
      END SUBROUTINE W3DIMUG
           
!/
!/ End of module W3GDATMD -------------------------------------------- /
!/
      END MODULE W3GDATMD
